import "@flo/io"
import "@flo/collection/map"
import "types"
import "utils"
import "error"
import "lexer"
import "debug"
import "parser"
import "typechecker"
import "codegen"
fnc parse_args(size: int, args: i8**): string[] {
    let arr: string[] = []
    for let i = 0; i < size; i++
        arr << string_from_cstring(args[i])
    return arr
}
fnc help(exec_file: string){
    println("Usage:", exec_file, "<path> [OPTIONS]\n")
    println("OPTIONS:")
    println(" -h, --help\t\t\tShow this help message.")
    println(" --print-tokens\t\t\tPrint generated Tokens")
    println(" --print-ast\t\t\tPrint generated AST.")
    println(" --print-llvm\t\t\tPrint generated LLVM-IR.")
    println(" -o <file>\t\t\tPlace the output into <file>.")
    println(" --no-output\t\t\tCompile with no output.")
    println(" --target\t\t\tCompile to specific target triple.")
    println(" -O OPT_LEVEL\n \t\t\t\tSpecify the compiler's optimization level which is a\n \t\t\t\tvalue from 0-3.")
    println(" -e, --execute\t\t\tExecute file after compiling.")
    println(" -v, --version\t\t\tShow version.")
}
fnc flag(args: string[], name: string): bool {
    return name in args
}
fnc flag_value_or(args: string[], name: string, default: string): string {
    let idx = args.find(name)
    if idx != -1 {
        if args.length > ++idx
            return args[idx]
        else {
            print_message_error(ErrorKind.GEN, "Missing value after '"+name+"' argument.")
            exit()
        }
    }
    return default
}
fnc main(argc: int, argv: i8**): int {
    let args = parse_args(argc, argv)
    if args.length >= 2 {
        let errors: FloError[] = []
        if flag(args, "-v") or flag(args, "--version"){
            println("v0.01@test.selfhost")
            return 0
        }
        if flag(args, "-h") or flag(args, "--help"){
            help(args[0])
            return 0
        }
        let filename = args[1]
        let file = new File(filename)
        if !file.exists(){
            print_message_error(ErrorKind.IO, "No such file or directory: '" + filename + "'")
            return 1
        }
        let text = file.read_as_string()
        /***** Lexing ******/
        let lexer: Lexer
        let tokens = lexer.tokenize(text, errors)
        if errors.length > 0 {
            print_errors(errors, filename, text)
            return 1
        }
        if flag(args, "--print-tokens")
            print_tokens(tokens, text)
        /***** Parsing *****/
        let parser: Parser
        let module_node = parser.parse(tokens, errors)
        if errors.length > 0 {
            print_errors(errors, filename, text)
            return 1
        }
        /**** Analyzing ****/
        let typechecker: TypeChecker
        let program = typechecker.check(module_node, filename, errors)
        if errors.length > 0 {
            print_error(errors[0], filename, text)
            return 1
        }        
        /**** Codegen ****/
        let codegen: CodeGen
        let flo_module = codegen.codegenProgram(program)
        flo_module.run_passes(flag_value_or(args, "-O", "1") as int)
        if flag(args, "--print-llvm")
            flo_module.print()
        if !flag(args, "--no-output") {
            flo_module.write_object(flag_value_or(args, "-o", "a.o"), flag_value_or(args, "--target", ""))
        }
        if flag(args, "-e") or flag(args, "--execute") {
            flo_module.exec()
        }
        flo_module.destroy()
    } else {
        print_message_error(ErrorKind.GEN, "No input file specified.")
        return 1
    }
    return 0
}