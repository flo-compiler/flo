import "llvm/types"
import panic in "utils"
import LLVMVerifyModule in "llvm/analysis"
import LLVMCreatePassManager, LLVMGetFirstFunction, LLVMGetNextFunction, LLVMPrintModuleToFile, LLVMShutdown, LLVMSetTarget, LLVMSetDataLayout, LLVMDisposeMessage, LLVMCreateFunctionPassManagerForModule, LLVMRunFunctionPassManager, LLVMRunPassManager, LLVMDisposePassManager, LLVMGetNamedFunction in "llvm/core"
import LLVMRemoveModule, LLVMLinkInMCJIT, LLVMCreateExecutionEngineForModule, LLVMDisposeExecutionEngine, LLVMRunFunctionAsMain in "llvm/executionEngine"
import LLVMPassManagerBuilderPopulateFunctionPassManager, LLVMPassManagerBuilderUseInlinerWithThreshold, LLVMPassManagerBuilderDispose, LLVMPassManagerBuilderCreate, LLVMPassManagerBuilderSetOptLevel, LLVMPassManagerBuilderPopulateModulePassManager in "llvm/passManagerBuilder"
import LLVMAddInternalizePass, LLVMAddInstructionCombiningPass, LLVMAddGlobalDCEPass, LLVMAddGlobalOptimizerPass, LLVMAddScalarReplAggregatesPass, LLVMAddFunctionInliningPass in "llvm/transforms"
import LLVM_InitializeAllTargetInfos, LLVM_InitializeAllTargetMCs, LLVM_InitializeAllTargets, LLVM_InitializeAllAsmParsers, LLVM_InitializeAllAsmPrinters in "llvm/target"
import LLVMDisposeTargetMachine, LLVMTargetMachineEmitToFile, LLVMGetDefaultTargetTriple, LLVMGetTargetMachineTriple, LLVMCreateTargetDataLayout, LLVMCreateTargetMachine, LLVMGetTargetFromTriple, LLVMGetHostCPUFeatures in "llvm/targetMachine"
class GeneratedModule {
    private llvm_mod: LLVMModuleRef
    private errors: i8*
    constructor(this, llvm_mod: LLVMModuleRef){
        this.llvm_mod = llvm_mod
        this.errors = null
        if LLVMVerifyModule(this.llvm_mod, LLVMVerifierFailureAction.LLVMPrintMessageAction, &this.errors) {
            this.print()
            this.print_errors()
        }
        LLVM_InitializeAllTargetInfos()
        LLVM_InitializeAllTargets()
        LLVM_InitializeAllTargetMCs()
        LLVM_InitializeAllAsmParsers()
        LLVM_InitializeAllAsmPrinters()
    }
    private print_errors(this){
        let error = string.from_cstring(this.errors)
        LLVMDisposeMessage(this.errors)
        panic("error: $error")
    }
    public run_passes(this, level: int){
        if level == 0 return
        let pm = LLVMCreatePassManager()
        let fpm = LLVMCreateFunctionPassManagerForModule(this.llvm_mod)
        let pmb = LLVMPassManagerBuilderCreate()
        LLVMAddInternalizePass(pm, 1)
        LLVMAddGlobalDCEPass(pm)
        LLVMAddGlobalOptimizerPass(pm)
        LLVMAddInstructionCombiningPass(pm)
        LLVMAddScalarReplAggregatesPass(pm)
        LLVMAddFunctionInliningPass(pm)
        LLVMPassManagerBuilderSetOptLevel(pmb, level)
        LLVMPassManagerBuilderUseInlinerWithThreshold(pmb, 255)
        LLVMPassManagerBuilderPopulateFunctionPassManager(pmb, fpm)
        let func = LLVMGetFirstFunction(this.llvm_mod)
        while func != null {
            LLVMRunFunctionPassManager(fpm, func)
            func = LLVMGetNextFunction(func)
        }
        LLVMPassManagerBuilderPopulateModulePassManager(pmb, pm)
        LLVMRunPassManager(pm, this.llvm_mod)
        LLVMDisposePassManager(fpm)
        LLVMPassManagerBuilderDispose(pmb)
        LLVMDisposePassManager(pm)
    }
    public exec(this): int {
        LLVMLinkInMCJIT()
        let engine: LLVMExecutionEngineRef = null
        if LLVMCreateExecutionEngineForModule(&engine, this.llvm_mod, &this.errors) {
            this.print_errors()
        }
        let main_fnc = LLVMGetNamedFunction(this.llvm_mod, "main")
        if main_fnc == null {
            println("Main function not found in module; can't execute")
            LLVMDisposeExecutionEngine(engine)
            return 1
        }
        let ret = LLVMRunFunctionAsMain(engine, main_fnc, 0, null, null)
        if LLVMRemoveModule(engine, this.llvm_mod, &this.llvm_mod, &this.errors) {
            this.print_errors()
        }
        LLVMDisposeExecutionEngine(engine)
        return ret
    }
    private create_target_machine(this, triple_str: string): LLVMTargetMachineRef {
        let triple = triple_str == "" ? LLVMGetDefaultTargetTriple() : triple_str.to_cstring()
        let target: LLVMTargetRef = null
        if LLVMGetTargetFromTriple(triple, &target, &this.errors) {
            this.print_errors()
        }
        let CPU: i8* = "generic"
        return LLVMCreateTargetMachine(target, triple, CPU, LLVMGetHostCPUFeatures(), LLVMCodeGenOptLevel.LLVMCodeGenLevelAggressive, LLVMRelocMode.LLVMRelocDefault, LLVMCodeModel.LLVMCodeModelDefault)
    }
    public write_object(this, fn: string, target: string){
        let tm = this.create_target_machine(target)
        LLVMSetDataLayout(this.llvm_mod, LLVMCreateTargetDataLayout(tm))
        LLVMSetTarget(this.llvm_mod, LLVMGetTargetMachineTriple(tm))
        if LLVMTargetMachineEmitToFile(tm, this.llvm_mod, fn.to_cstring(), LLVMCodeGenFileType.LLVMObjectFile, &this.errors) {
            this.print_errors()
        }
        LLVMDisposeTargetMachine(tm)
    }
    public print(this){
        if LLVMPrintModuleToFile(this.llvm_mod, "output.ll", &this.errors) {
            this.print_errors()
        }
    }
    public llvm_shutdown(this){
        LLVMShutdown()
    }
}