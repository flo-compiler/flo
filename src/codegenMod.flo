import "llvm/types"
import panic in "utils"
import LLVMVerifyModule in "llvm/analysis"
import LLVMCreatePassManager, LLVMGetFirstFunction, LLVMGetNextFunction, LLVMPrintModuleToFile, LLVMShutdown, LLVMSetTarget, LLVMSetDataLayout, LLVMDisposeMessage, LLVMCreateFunctionPassManagerForModule, LLVMRunFunctionPassManager, LLVMRunPassManager, LLVMDisposePassManager, LLVMGetNamedFunction in "llvm/core"
import LLVMRemoveModule, LLVMLinkInMCJIT, LLVMCreateExecutionEngineForModule, LLVMDisposeExecutionEngine, LLVMRunFunctionAsMain in "llvm/executionEngine"
import LLVMPassManagerBuilderPopulateFunctionPassManager, LLVMPassManagerBuilderUseInlinerWithThreshold, LLVMPassManagerBuilderDispose, LLVMPassManagerBuilderCreate, LLVMPassManagerBuilderSetOptLevel, LLVMPassManagerBuilderPopulateModulePassManager in "llvm/passManagerBuilder"
import LLVMAddInternalizePass, LLVMAddInstructionCombiningPass, LLVMAddGlobalDCEPass, LLVMAddGlobalOptimizerPass, LLVMAddScalarReplAggregatesPass, LLVMAddFunctionInliningPass in "llvm/transforms"
import LLVM_InitializeAllTargetInfos, LLVM_InitializeAllTargetMCs, LLVM_InitializeAllTargets, LLVM_InitializeAllAsmParsers, LLVM_InitializeAllAsmPrinters in "llvm/target"
import LLVMDisposeTargetMachine, LLVMTargetMachineEmitToFile, LLVMGetTargetMachineTriple, LLVMCreateTargetDataLayout, LLVMCreateTargetMachine, LLVMGetTargetFromTriple, LLVMGetHostCPUFeatures in "llvm/targetMachine"
import LLVMWriteBitcodeToFile in "llvm/bitwriter"
class GeneratedModule {
    private llvm_mod: LLVMModuleRef
    private errors: u8*
    private tm: LLVMTargetMachineRef
    constructor(this, llvm_mod: LLVMModuleRef){
        this.llvm_mod = llvm_mod
        this.errors = null
        if LLVMVerifyModule(this.llvm_mod, LLVMVerifierFailureAction.LLVMPrintMessageAction, &this.errors) {
            this.write_ir("error.ll")
            this.print_errors()
        }
        LLVM_InitializeAllTargetInfos()
        LLVM_InitializeAllTargets()
        LLVM_InitializeAllTargetMCs()
        LLVM_InitializeAllAsmParsers()
        LLVM_InitializeAllAsmPrinters()
    }
    private print_errors(this){
        let error = string.from_cstring(this.errors)
        LLVMDisposeMessage(this.errors)
        panic("error: $error")
    }
    public run_passes(this, level: int){
        if level == 0 return
        let pm = LLVMCreatePassManager()
        let fpm = LLVMCreateFunctionPassManagerForModule(this.llvm_mod)
        let pmb = LLVMPassManagerBuilderCreate()
        LLVMAddInternalizePass(pm, 1)
        LLVMAddGlobalDCEPass(pm)
        LLVMAddGlobalOptimizerPass(pm)
        LLVMAddInstructionCombiningPass(pm)
        LLVMAddScalarReplAggregatesPass(pm)
        LLVMAddFunctionInliningPass(pm)
        LLVMPassManagerBuilderSetOptLevel(pmb, level)
        LLVMPassManagerBuilderUseInlinerWithThreshold(pmb, 255)
        LLVMPassManagerBuilderPopulateFunctionPassManager(pmb, fpm)
        let func = LLVMGetFirstFunction(this.llvm_mod)
        while func != null {
            LLVMRunFunctionPassManager(fpm, func)
            func = LLVMGetNextFunction(func)
        }
        LLVMPassManagerBuilderPopulateModulePassManager(pmb, pm)
        LLVMRunPassManager(pm, this.llvm_mod)
        LLVMDisposePassManager(fpm)
        LLVMPassManagerBuilderDispose(pmb)
        LLVMDisposePassManager(pm)
    }
    public exec(this): int {
        LLVMLinkInMCJIT()
        let engine: LLVMExecutionEngineRef = null
        if LLVMCreateExecutionEngineForModule(&engine, this.llvm_mod, &this.errors) {
            this.print_errors()
        }
        let main_fnc = LLVMGetNamedFunction(this.llvm_mod, "main")
        if main_fnc == null {
            panic("Main function not found in module; can't execute")
            LLVMDisposeExecutionEngine(engine)
            return 1
        }
        let ret = LLVMRunFunctionAsMain(engine, main_fnc, 0, null, null)
        if LLVMRemoveModule(engine, this.llvm_mod, &this.llvm_mod, &this.errors) {
            this.print_errors()
        }
        LLVMDisposeExecutionEngine(engine)
        return ret
    }
    public set_target_machine(this, triple_str: string){
        let triple: u8*(triple_str.length + 1)
        triple.copy_from(triple_str.get_bytes(), triple_str.length)
        triple[triple_str.length] = 0
        let target: LLVMTargetRef = null
        if LLVMGetTargetFromTriple(triple, &target, &this.errors) {
            this.print_errors()
        }
        this.tm = LLVMCreateTargetMachine(target, triple, "generic", LLVMGetHostCPUFeatures(), LLVMCodeGenOptLevel.LLVMCodeGenLevelAggressive, LLVMRelocMode.LLVMRelocDefault, LLVMCodeModel.LLVMCodeModelDefault)
        LLVMSetDataLayout(this.llvm_mod, LLVMCreateTargetDataLayout(this.tm))
        LLVMSetTarget(this.llvm_mod, LLVMGetTargetMachineTriple(this.tm))
    }
    public write_obj(this, filename: string){
        let filename_cstr = filename.to_cstring()
        if LLVMTargetMachineEmitToFile(this.tm, this.llvm_mod, filename_cstr, LLVMCodeGenFileType.LLVMObjectFile, &this.errors) {
            this.print_errors()
        }
        del filename_cstr
        del filename
    }
    public write_ir(this, filename: string){
        let filename_cstr = filename.to_cstring()
        if LLVMPrintModuleToFile(this.llvm_mod, filename_cstr, &this.errors) {
            this.print_errors()
        }
        del filename_cstr
        del filename
    }
    public write_bc(this, filename: string) {
        let filename_cstr = filename.to_cstring()
        LLVMWriteBitcodeToFile(this.llvm_mod, filename_cstr)
        del filename_cstr
        del filename
    }
    public write_asm(this, filename: string) {
        let filename_cstr = filename.to_cstring()
        if LLVMTargetMachineEmitToFile(this.tm, this.llvm_mod, filename_cstr, LLVMCodeGenFileType.LLVMAssemblyFile, &this.errors){
            this.print_errors()
        }
        del filename_cstr
        del filename
    }
    private __del__(this){
        LLVMShutdown()
        LLVMDisposeTargetMachine(this.tm)
    }
}