import TokType in "types"
import FloRange in "utils"
import FloError, ErrorKind in "error"
fnc string_to_kw_token(word: string): TokType {
    if word == "and"
        return TokType.AND_KW
    if word == "or"
        return TokType.OR_KW
    if word == "const"
        return TokType.CONST_KW
    if word == "let"
        return TokType.LET_KW
    if word == "if" 
        return TokType.IF_KW
    if word == "match" 
        return TokType.MATCH_KW
    if word == "else"
        return TokType.ELSE_KW
    if word == "for"
        return TokType.FOR_KW
    if word == "while"
        return TokType.WHILE_KW
    if word == "fnc"
        return TokType.FNC_KW
    if word == "break"
        return TokType.BREAK_KW
    if word == "continue"
        return TokType.CONTINUE_KW
    if word == "return"
        return TokType.RETURN_KW
    if word == "import"
        return TokType.IMPORT_KW
    if word == "private"
        return TokType.PRIVATE_KW
    if word == "public"
        return TokType.PUBLIC_KW
    if word == "protected"
        return TokType.PROTECTED_KW
    if word == "in"
        return TokType.IN_KW
    if word == "class"
        return TokType.CLASS_KW
    if word == "enum"
        return TokType.ENUM_KW
    if word == "new"
        return TokType.NEW_KW
    if word == "del"
        return TokType.DEL_KW
    if word == "as"
        return TokType.AS_KW
    return TokType.ILLEGAL
}
fnc is_identifier_char(char: u8): bool {
    return (char >= 'A' and char <= 'Z') or (char >= 'a' and char <= 'z') or (char == '_')
}
fnc is_number_char(char: u8): bool {
    return (char >= '0' and char <= '9')
}
fnc capitalize(char: u8): u8 {
    if char >= 'a' and char <= 'z'
        return char - ' '
    return char 
}
fnc in_base_range(char: u8, base: int): bool {
    if base < 10 
        return char >= '0' and char <= (base + 48)
    return (char >= '0' and char <= (base + 48)) or (char >= 'A' and char <= (base + 65))
}
fnc get_escape_char(char: u8): u8 {
    if (char == 'a') return '\a'
    if (char == 'b') return '\b'
    if (char == 't') return '\t'
    if (char == 'f') return '\f'
    if (char == 'n') return '\n'
    if (char == 'r') return '\r'
    if (char == 'v') return '\v'
    if (char == '0') return '\0'
    if (char == '\'') return '\''
    if (char == '\"') return '\"'
    if (char == '\\') return '\\'
    return char
}
fnc unescape(str: string): string {
    let str_builder: StringBuilder(str.length)
    let char = '\0'
    let aval = &char
    for let i = 0; i < str.length; i++ {
        char = str.get_byte(i)
        if (char == '\\') {
            if i + 3 < str.length {
                aval = str.get_bytes() + i + 1
                if (aval.compare("033", 3) == 0) or (aval.compare("x1b", 3) == 0) {
                    str_builder.append_string("\033")
                    i += 3
                    continue
                }
                if i + 6 < str.length {
                    if aval.compare("u001b", 5) == 0 {
                        str_builder.append_string("\033")
                        i += 5
                        continue
                    }
                }
            }
            char = get_escape_char(str.get_byte(++i))
        }
        str_builder.add_byte(char)
    }
    return str_builder.get_string()
}
class Token {
    ttype: TokType
    range: FloRange
    constructor(this, ttype: TokType, range: FloRange){
        this.ttype = ttype
        this.range = range
    }
}
class IntToken (Token) {
    value: u64
    constructor(this, value: u64, range: FloRange){
        super(TokType.INT, range)
        this.value = value
    }
}
class FloatToken (Token) {
    value: f64
    constructor(this, value: f64, range: FloRange){
        super(TokType.FLOAT, range)
        this.value = value
    }
}
class CharToken (Token) {
    value: u8
    constructor(this, value: u8, range: FloRange){
        super(TokType.CHAR, range)
        this.value = value
    }
}
class StrToken (Token) {
    value: string
    fmt_indices: uint[]
    args: Token[][]
    constructor(this, args: Token[][], fmt_indices: uint[], value: string, range: FloRange){
        super(TokType.STR, range)
        this.value = value
        this.fmt_indices = fmt_indices
        this.args = args
    }
}
class IdentifierToken (Token) {
    value: string
    constructor(this, value: string, range: FloRange){
        super(TokType.IDENTIFER, range)
        this.value = value
    }
}
class Lexer {
    private current_char: u8
    private text: string
    private errors: FloError[]
    private idx: int
    private col: uint
    private line: uint
    constructor(this, text: string, errors: FloError[]){
        this.text = text
        this.errors = errors
        this.idx = -1
        this.line = 1
        this.col = 0
    }
    private eof(this): bool{
        return this.idx >= this.text.length
    }
    private peek(this, offset = 1): u8 {
        if this.idx + offset < this.text.length 
            return this.text.get_byte((this.idx + offset) as uint)
        return '\0'
    }
    private advance(this){
        if this.eof() return
        this.current_char = this.text.get_byte(++this.idx as uint)
        if this.current_char == '\n' {
            this.col = 0
            this.line++
        } else
            this.col++
    }
    private get_range(this, back: uint): FloRange {
        return new FloRange(this.idx - back + 1, (this.idx + 1) as uint, this.col - back, this.line)
    }
    private plus_token(this): Token {
        if this.peek() == '+' {
            this.advance()
            return new Token(TokType.PLUS_PLUS, this.get_range(2))
        } else if this.peek() == '=' {
            this.advance()
            return new Token(TokType.PLUS_EQ, this.get_range(2))
        }
        return new Token(TokType.PLUS, this.get_range(1))
    }
    private skip_comment(this): bool{
        if this.peek() == '*' {
            this.advance()
            while !this.eof() {
                if this.peek() == '*' and this.peek(2) == '/' break
                this.advance()
            }
            this.advance()
            this.advance()
            return true
        } else if this.peek() == '/' {
            this.advance()
            while this.peek() != '\n' and !this.eof()
                this.advance()
            return true
        }
        return false
    }
    private minus_token(this): Token{
        if this.peek() == '-' {
            this.advance()
            return new Token(TokType.MINUS_MINUS, this.get_range(2))
        } else if this.peek() == '=' {
            this.advance()
            return new Token(TokType.MINUS_EQ, this.get_range(2))
        } else if this.peek() == '>' {
            this.advance()
            return new Token(TokType.ARROW, this.get_range(2))
        }
        return new Token(TokType.MINUS, this.get_range(2))
    }
    private mult_token(this): Token {
        if this.peek() == '=' {
            this.advance()
            return new Token(TokType.MULT_EQ, this.get_range(2))
        } else if this.peek() == '*' {
            this.advance()
            if this.peek() == '=' {
                this.advance()
                return new Token(TokType.POW_EQ, this.get_range(3))
            }
            return new Token(TokType.POW, this.get_range(2))
        }
        return new Token(TokType.MULT, this.get_range(1))
    }
    private mod_token(this): Token {
        if this.peek() == '=' {
            this.advance()
            return new Token(TokType.MOD_EQ, this.get_range(2))
        }
        return new Token(TokType.MOD, this.get_range(1))
    }
    private xor_token(this): Token {
        if this.peek() == '=' {
            this.advance()
            return new Token(TokType.XOR_EQ, this.get_range(2))
        }
        return new Token(TokType.XOR, this.get_range(1))
    }
    private div_token(this): Token {
        if this.peek() == '=' {
            this.advance()
            return new Token(TokType.DIV_EQ, this.get_range(2))
        }
        return  new Token(TokType.DIV, this.get_range(1))
    }
    private eq_token(this): Token {
        if this.peek() == '='{
            this.advance()
            return new Token(TokType.EEQ, this.get_range(2))
        }
        return new Token(TokType.EQ, this.get_range(1))
    }
    private neq_token(this): Token {
        if this.peek() == '=' {
            this.advance()
            return new Token(TokType.NEQ, this.get_range(2))
        }
        return new Token(TokType.NOT, this.get_range(1))
    }
    private gt_token(this): Token {
        if this.peek() == '=' {
            this.advance()
            return new Token(TokType.GTE, this.get_range(2))
        }
        else if this.peek() == '>' {
            this.advance()
            if this.peek() == '=' {
                this.advance()
                return new Token(TokType.SR_EQ, this.get_range(3))
            }
            return new Token(TokType.SR, this.get_range(2))
        }
        return new Token(TokType.GT, this.get_range(1))
    }
    private lt_token(this): Token {
        if this.peek() == '=' {
            this.advance()
            return new Token(TokType.LTE, this.get_range(2))
        }
        else if this.peek() == '<' {
            this.advance()
            if this.peek() == '=' {
                this.advance()
                return new Token(TokType.SL_EQ, this.get_range(3))
            }
            return new Token(TokType.SL, this.get_range(2))
        }
        return new Token(TokType.LT, this.get_range(1))
    }
    private dot_token(this): Token {
        let token = TokType.DOT
        let length = 1
        if this.peek() == '.'{
            token = TokType.DOT_DOT
            length++
            this.advance()
            if this.peek() == '.' {
                token = TokType.DOT_DOT_DOT
                length++
                this.advance()
            }
        }
        return new Token(token, this.get_range(length))
    }
    private base_int_token(this, base: u64): IntToken{
        this.advance()
        let length = 3
        let value: u64 = 0
        let current_char = '\0'
        while in_base_range(current_char = capitalize(this.peek()), base as int) and !this.eof() {
            value *= base
            let add =  current_char - '0'
            if (current_char >= 'A') add = current_char - 'A' + 10
            value += add
            length++
            this.advance()
        }
        return new IntToken(value, this.get_range(length))
    }
    private numeric_token(this): Token {
        let length = 1
        let is_float = false
        let next_char = '\0'
        if this.current_char == '0' and this.peek() == 'b'
            return this.base_int_token(2)
        if this.current_char == '0' and this.peek() == 'o'
            return this.base_int_token(8)
        if this.current_char == '0' and this.peek() == 'x'
            return this.base_int_token(16)
        while (is_number_char(next_char = this.peek()) or next_char == '.') and !this.eof() {
            if next_char == '.' and (is_float or !is_number_char(this.peek(2))) break
            if this.current_char == '.' is_float = true
            length++
            this.advance()
        }
        let range = this.get_range(length)
        let str_value = this.text.substring(range.start, length)
        let num_value: Token = is_float ? new FloatToken(str_value as f64, range) as Token : new IntToken((str_value as u64), range)
        del str_value
        return num_value
    }
    private identifier_token(this): Token {
        let length = 1
        let start = this.idx
        let next_char = this.peek()
        while (is_identifier_char(next_char = this.peek()) or is_number_char(next_char)) and !this.eof() {
            length++
            this.advance()
        }
        let value = this.text.substring(start, length)
        let range = this.get_range(length)
        let keyword: TokType = TokType.ILLEGAL
        if (keyword = string_to_kw_token(value)) != TokType.ILLEGAL { 
            if this.peek() == '=' {
                if keyword == TokType.AND_KW {
                    this.advance()
                    keyword = TokType.AND_EQ
                } 
                if keyword == TokType.OR_KW {
                    this.advance()
                    keyword = TokType.OR_EQ
                }
            }
            return new Token(keyword, range)
        }
        return new IdentifierToken(value, range)
    }
    private string_token(this): StrToken {
        let length = 2
        let escape_next = false
        let args: Token[][] = []
        let fmt_indices: uint[] = []
        let aval: i8* = null
        this.advance()
        let str_builder: StringBuilder(1)
        while !this.eof() {
            if (this.current_char == '"' and !escape_next) break
            if (this.current_char == '$' and !escape_next){
                fmt_indices << length - 2
                this.advance()
                let token_group: Token[] = []
                let active_open_pars = 0
                if this.current_char == '(' {
                    this.advance()
                    while (this.current_char != ')' or active_open_pars != 0) and !this.eof(){
                        if this.current_char == '(' active_open_pars++
                        if this.current_char == ')' active_open_pars--
                        token_group << this.lex_token()
                        this.advance()
                    }
                } else {
                    token_group << this.lex_token()
                }
                this.advance()
                token_group << new Token(TokType.EOF, new FloRange(0, 0, this.col, this.line))
                args << token_group
                continue
            }
            length++
            escape_next = this.current_char == '\\'
            str_builder.add_byte(this.current_char)
            this.advance()
        }
        let range = this.get_range(length)
        let value = unescape(str_builder.get_string())
        return new StrToken(args, fmt_indices, value, range)
    }
    private char_token(this): CharToken {
        let length = 3
        this.advance()
        let value = this.current_char
        if this.current_char == '\\' {
            this.advance()
            value = get_escape_char(this.current_char)
            length += 2
        }
        this.advance()
        return new CharToken(value, this.get_range(length))
    }
    private lex_token(this): Token {
        let char = this.current_char
        if this.current_char == ' ' or this.current_char == '\t'  {
            this.advance()
            return this.lex_token()
        }
        if is_identifier_char(char) 
            return this.identifier_token()
        if char == '"'
            return this.string_token()
        if is_number_char(char)
            return this.numeric_token()
        if char == ':' 
            return new Token(TokType.COL, this.get_range(1))
        if char == ';'
            return new Token(TokType.SEMICOL, this.get_range(1))
        if char == ','
            return new Token(TokType.COMMA, this.get_range(1))
        if char == '+'
            return this.plus_token()
        if char == '-'
            return this.minus_token()
        if char == '*'
            return this.mult_token()
        if char == '%'
            return this.mod_token()
        if char == '^'
            return this.xor_token()
        if char == '/' {
            if this.skip_comment() {
                this.advance()
                return this.lex_token()
            }
            return this.div_token()
        } 
        if char == '('
            return new Token(TokType.LPAR, this.get_range(1))
        if char == ')'
            return new Token(TokType.RPAR, this.get_range(1))
        if char == '{'
            return new Token(TokType.LBRACE, this.get_range(1))
        if char == '}'
            return new Token(TokType.RBRACE, this.get_range(1))
        if char == '['
            return new Token(TokType.LBRACKET, this.get_range(1))
        if char == ']'
            return new Token(TokType.RBRACKET, this.get_range(1))
        if char == '\n' or char == '\r'
            return new Token(TokType.LN, this.get_range(1))
        if char == '\''
            return (this.char_token() as Token)
        if char == '?'
            return new Token(TokType.QUES, this.get_range(1))
        if char == '&'
            return new Token(TokType.AMP, this.get_range(1))
        if char == '='
            return this.eq_token()
        if char == '!'
            return this.neq_token()
        if char == '>'
            return this.gt_token()
        if char == '<'
            return this.lt_token()
        if char == '.'
            return this.dot_token()
        if char == '\0'
            return new Token(TokType.EOF, new FloRange(0, 0, this.col, this.line))
        let char_msg = this.text.substring(this.idx, 1)
        this.errors << new FloError(ErrorKind.ILLEGALCHAR, "Illegal character: '$char_msg'", this.get_range(1))
        del char_msg
        return null
    }
    public tokenize(text: string, errors: FloError[]): Token[] {
        let lexer: Lexer(text, errors)
        lexer.advance()
        let tokens: Token[] = []
        while !lexer.eof() and lexer.errors.length == 0 {
            tokens << lexer.lex_token()
            lexer.advance()
        }
        tokens << new Token(TokType.EOF, new FloRange(0, 0, lexer.col, lexer.line))
        return tokens
    }
}