import "llvm"
$WORD_INT_TYPE = LLVMIntType($WORD_SIZE)
class GeneratedModule {
    private llvm_mod: LLVMModuleRef
    private errors: i8*
    constructor(this, llvm_mod: LLVMModuleRef){
        this.llvm_mod = llvm_mod
        this.errors = null
        LLVMVerifyModule(this.llvm_mod, LLVMVerifierFailureAction.LLVMAbortProcessAction, &this.errors)
        this.check_errors()
        LLVMLinkInMCJIT()
        LLVMInitializeAllTargets()
        LLVMInitializeAllTargetInfos()
        LLVMInitializeAllTargetMCs()
        LLVMInitializeAllTargetPrinters()
        LLVMInitializeAllTargetParsers()
    }
    private check_errors(this){
        if (this.errors[0] != 0) {
            println("error:", string_from_cstring(this.errors))
            LLVMDisposeMessage(this.errors)
            exit()
        }
    }
    public run_passes(this, level: int){
        let fpm = LLVMCreateFunctionPassManagerForModule(this.llvm_mod)
        // let mpm = LLVMCreatePassManager()
        let pmb = LLVMPassManagerBuilderCreate()
        LLVMPassManagerBuilderSetOptLevel(pmb, level)
        LLVMPassManagerBuilderUseInlinerWithThreshold(pmb, 255)
        // LLVMPassManagerBuilderPopulateModulePassManager(pmb, mpm)
        let func = LLVMGetFirstFunction(this.llvm_mod)
        LLVMPassManagerBuilderPopulateFunctionPassManager(pmb, fpm)
        while func != null {
            LLVMRunFunctionPassManager(fpm, func)
            func = LLVMGetNextFunction(func)
        }
        // LLVMRunPassManager(mpm, this.llvm_mod)
        LLVMDisposePassManager(fpm)
        // LLVMDisposePassManager(mpm)
        LLVMPassManagerBuilderDispose(pmb)
    }
    public exec(this){
        let module_copy = this.copy_llvm_module()
        let engine: LLVMExecutionEngineRef = null
        if (LLVMCreateExecutionEngineForModule(&engine, module_copy, &this.errors) != 0) {
            println("failed to create execution engine")
            return
        }
        let main_fnc = LLVMGetFunctionAddress(engine, "main")  as () => int
        if main_fnc == null {
            println("Main function not found in module; can't execute")
            LLVMDisposeExecutionEngine(engine)
            return
        }
        println("Exited with code:", main_fnc())
        LLVMDisposeExecutionEngine(engine)
    }
    public create_target_machine(this, triple_str: string): LLVMTargetMachineRef {
        let triple = triple_str == "" ? LLVMGetDefaultTargetTriple() : triple_str.to_cstring()
        let target = LLVMGetFirstTarget()
        if (LLVMGetTargetFromTriple(triple, &target, &this.errors) != 0) {
            println("failed to get target triple:", triple_str)
        }
        this.check_errors()
        let CPU: i8* = "generic"
        return LLVMCreateTargetMachine(target, triple, CPU, LLVMGetHostCPUFeatures(), LLVMCodeGenOptLevel.LLVMCodeGenLevelAggressive, LLVMRelocMode.LLVMRelocDefault, LLVMCodeModel.LLVMCodeModelDefault)
    }
    public write_object(this, fn: string, target: string){
        let tm = this.create_target_machine(target)
        let module_copy = this.copy_llvm_module()
        LLVMSetDataLayout(module_copy, LLVMCreateTargetDataLayout(tm))
        LLVMSetTarget(module_copy, LLVMGetTargetMachineTriple(tm))
        if (LLVMTargetMachineEmitToFile(tm, module_copy, fn.to_cstring(), 1, &this.errors) != 0) {
            println("target machine failed to emit object code")
        }
        this.check_errors()
        LLVMDisposeTargetMachine(tm)
    }
    public print(this){
        LLVMDumpModule(this.llvm_mod)
    }
    private copy_llvm_module(this): LLVMModuleRef{
        return LLVMCloneModule(this.llvm_mod)
    }
    public destroy(this){
        let ctx = LLVMGetModuleContext(this.llvm_mod)
        LLVMDisposeModule(this.llvm_mod)
        LLVMContextDispose(ctx)
        LLVMShutdown()
    }
}
class IntrinsicResult {
    call: LLVMValueRef
    fnc_type: LLVMTypeRef
}
class StructClass  {
    llvm_type: LLVMTypeRef
    vt_methods_pos_map: Map<string, int>
    fields_pos_map: Map<string, int>
    non_vt_methods_map: Map<string, LLVMValueRef>
    vtable: LLVMValueRef
    constructor: LLVMValueRef
    fields_num: int
    methods_num: int
    static_methods_num: int
    static_fields_num: int
}
fnc get_intrinsic(mod: LLVMModuleRef, name: string, params: LLVMTypeRef[]): IntrinsicResult {
    let result: IntrinsicResult
    result.fnc_type = LLVMFunctionType(params[0], params.buffer, params.length, false)
    result.call = LLVMAddFunction(mod, name.to_cstring(), result.fnc_type)
    return result
}
fnc createLLVMBool(val: bool): LLVMValueRef{
    return LLVMConstInt(LLVMInt1Type(), val as int, false)
}
fnc createLLVMMachineSizeInt(i: int): LLVMValueRef {
    return LLVMConstInt($WORD_INT_TYPE, i, false)
}

$ZERO = createLLVMMachineSizeInt(0)
$ONE = createLLVMMachineSizeInt(1)
fnc is_terminated(block: Block): bool {
    return block.returns or block.continues or block.breaks
}
fnc create_var_key(var_id: VarId): string {
    return var_id.module_id + ";" + var_id.kind + ";" + var_id.id
}
fnc create_type_id_key(type_id: TypeId): string {
    return type_id.module_id + ";" + type_id.kind + ";" + type_id.id
}
fnc create_type_key_from_var(var_id: VarId): string {
    return var_id.module_id + ";" + (var_id.kind - VarIdKind.CLASS) + ";" + var_id.id
}
fnc create_prop_key(prop_id: PropId): string {
    return create_type_id_key(prop_id.type_id) + ";" + prop_id.kind + ";" + prop_id.id
}
fnc neg_int(builder: LLVMBuilderRef, val: LLVMValueRef): LLVMValueRef {
        return LLVMIsConstant(val) ? LLVMConstNeg(val) : LLVMBuildNeg(builder, val, "")
}
class CodeGen {
    modules: LLVMModuleRef[]
    module: LLVMModuleRef
    global_var_scope: Map<string, LLVMValueRef>
    module_intrinsic_scope: Map<string, IntrinsicResult>
    module_string_scope: Map<string, LLVMValueRef>
    module_classes: Map<string, StructClass>
    function: LLVMValueRef
    break_bb: LLVMBasicBlockRef
    continue_bb: LLVMBasicBlockRef
    program: Program
    in_static_ctx: bool
    module_ctx: LLVMContextRef
    codegenProgram(this, program: Program): GeneratedModule{
        this.program = program
        this.modules = []
        this.continue_bb = null
        this.break_bb = null
        let global_var_map: Map<string, LLVMValueRef>
        this.global_var_scope = global_var_map
        for let i = 0; i < program.modules.length; ++i {
            this.modules << this.codegen_module(program.modules[i])
        }
        this.link_modules()
        return new GeneratedModule(this.modules[0])
    }
    private link_modules(this){
        for let i = 1; i < this.modules.length; ++i {
            LLVMLinkModules2(this.modules[0], this.modules[i])
        }
    }
    private get_var_from_varid(var_id: VarId): LLVMValueRef{
        return this.global_var_scope.get(create_var_key(var_id))
    }
    private get_class_from_typeid(type_id: TypeId): StructClass {
        return this.module_classes.get(create_type_id_key(type_id))
    }
    private get_class_from_varid(var_id: VarId): StructClass {
        return this.module_classes.get(create_type_key_from_var(var_id))
    }
    private add_class(type_id: TypeId, struct_class: StructClass){
        this.module_classes.set(create_type_id_key(type_id), struct_class)
    }
    private add_var(var_id: VarId, value: LLVMValueRef){
        this.global_var_scope.set(create_var_key(var_id), value)
    }
    private codegen_module(this, module: Module): LLVMModuleRef {
        this.module_ctx = LLVMContextCreate()
        let module_string_scope: Map<string, LLVMValueRef>
        let module_intrinsic_scope: Map<string, IntrinsicResult>
        let module_classes: Map<string, StructClass>
        this.module_string_scope = module_string_scope
        this.module_intrinsic_scope = module_intrinsic_scope
        this.module_classes = module_classes
        this.module = LLVMModuleCreateWithNameInContext(module.name.to_cstring(), this.module_ctx)
        //let DBFloInfo = new DebugInfo(this.module, module.name)
        this.in_static_ctx = true
        for let i = 0; i < module.global_consts.length; ++i {
            this.codegen_const_declaration(null, module.global_consts[i])
        }
        for i = 0; i < module.global_vars.length; ++i {
            this.codegen_var_declaration(null, module.global_vars[i])
        }
        for i = 0; i < module.classes.length; ++i {
            this.declare_class(module.classes[i])
        }
        for i = 0; i < module.functions.length; ++i {
            this.declare_function(module.functions[i])
        }
        this.in_static_ctx = false
        for i = 0; i < module.classes.length; ++i {
            this.codegen_class(module.classes[i])
        }
        for i = 0; i < module.functions.length; ++i {
            this.codegen_function(module.functions[i])
        }
        return this.module
    }
    private codegen_type(this, tt: Type, as_pointer = true): LLVMTypeRef {
        if tt.kind == TypeKind.VOID
            return LLVMVoidTypeInContext(this.module_ctx)
        if tt.kind == TypeKind.BOOL
            return LLVMInt1TypeInContext(this.module_ctx)
        if tt.kind == TypeKind.I4
            return LLVMIntTypeInContext(this.module_ctx, 4)
        if tt.kind == TypeKind.I8
            return LLVMInt8TypeInContext(this.module_ctx)
        if tt.kind == TypeKind.I16
            return LLVMInt16TypeInContext(this.module_ctx)
        if tt.kind == TypeKind.I32
            return LLVMInt32TypeInContext(this.module_ctx)
        if tt.kind == TypeKind.INT
            return LLVMIntTypeInContext(this.module_ctx, $WORD_SIZE)
        if tt.kind == TypeKind.I64
            return LLVMInt64TypeInContext(this.module_ctx)
        if tt.kind == TypeKind.I128
            return LLVMInt128TypeInContext(this.module_ctx)
        if tt.kind == TypeKind.F16 
            return LLVMHalfTypeInContext(this.module_ctx)
        if tt.kind == TypeKind.F32
            return LLVMFloatTypeInContext(this.module_ctx)
        if tt.kind == TypeKind.F64 or tt.kind == TypeKind.FLOAT
            return LLVMDoubleTypeInContext(this.module_ctx)
        if tt.kind == TypeKind.PTR
            return LLVMPointerType(this.codegen_type((tt as PointerType).pointee_type))
        if tt.kind == TypeKind.ARRAY {
            let arr_ty = tt as ArrayType
            let element_type = this.codegen_type(arr_ty.element_type)
            return LLVMArrayType(element_type, arr_ty.length)
        }
        if tt.kind == TypeKind.FNC {
            let param_types: LLVMTypeRef[] = []
            let func_ty = tt as FunctionType
            for let i = 0; i < func_ty.arg_types.length; ++i {
                param_types << this.codegen_type(func_ty.arg_types[i])
            }
            let ret_type = this.codegen_type(func_ty.return_type)
            let llvm_fnty = LLVMFunctionType(ret_type, param_types.buffer, param_types.length, false)
            return as_pointer ? LLVMPointerType(llvm_fnty) : llvm_fnty
        }
        if tt.kind == TypeKind.OBJECT {
            let obj_type = this.get_class_from_typeid((tt as ObjectType).class_id)
            if obj_type != null {
                return as_pointer ? LLVMPointerType(obj_type.llvm_type) : obj_type.llvm_type
            }
        }
        // if tt.kind == TypeKind.ENUM
        // GENERIC_TEMPLATE
        // RESOLVED_GENERIC
        return LLVMInt32Type()
    }
    private codegen_return_stmt(builder: LLVMBuilderRef, return_stmt: ReturnStatement){
        if return_stmt.return_value != null 
            LLVMBuildRet(builder, this.codegen_expression(builder, return_stmt.return_value))
        else
            LLVMBuildRetVoid(builder)
    }
    private codegen_int(this, int_exp: IntExpression): LLVMValueRef {
        return LLVMConstInt(this.codegen_type(int_exp.value_type), int_exp.value, true)
    }
    private codegen_float(this, float_exp: FloatExpression): LLVMValueRef {
        return LLVMConstReal(this.codegen_type(float_exp.value_type), float_exp.value)
    }
    private codegen_bool(this, bool_exp: BooleanExpression): LLVMValueRef {
        let val = bool_exp.value
        return LLVMConstInt(LLVMInt1TypeInContext(this.module_ctx), val, false)
    }
    private codegen_string(this, builder: LLVMBuilderRef, string_exp: StringExpression): LLVMValueRef {
        let result = this.module_string_scope.get(string_exp.value)
        if result != null
            return result
        let str = LLVMBuildGlobalString(builder, string_exp.value.to_cstring(), "")
        this.module_string_scope.set(string_exp.value, str)
        return str
    }
    private store_array_elements(this, builder: LLVMBuilderRef, ptr: LLVMValueRef, array_exp: ArrayExpression): LLVMValueRef {
        let args = [$ZERO, $ZERO]
        let next_args = [$ONE]
        let array_type = this.codegen_type(array_exp.value_type)
        let elem_ty = LLVMPointerType(this.codegen_type((array_exp.value_type as ArrayType).element_type))
        let addr = LLVMBuildInBoundsGEP2(builder, array_type, ptr, &args[0], 2, "")
        for let i = 0; i < array_exp.value.length; ++i {
            LLVMBuildStore(builder, this.codegen_expression(builder, array_exp.value[i]), addr)
            addr = LLVMBuildInBoundsGEP2(builder, elem_ty, addr, &next_args[0], 1, "")
        }
        return addr
    }
    private codegen_array_exp(this, builder: LLVMBuilderRef, array_exp: ArrayExpression): LLVMValueRef {
        let elem_ty = this.codegen_type((array_exp.value_type as ArrayType).element_type)
        let elems: LLVMValueRef[] = []
        for let i = 0; i < array_exp.value.length; i++ {
            elems << this.codegen_expression(builder, array_exp.value[i])
        }
        return LLVMConstArray(elem_ty, elems.buffer, elems.length)
    }
    private get_var_expr_mem(this, builder: LLVMBuilderRef, var_exp: VarAccessExpression): LLVMValueRef {
        let ptr = this.get_var_from_varid(var_exp.value_id)
        if ptr == null {
            println("This should never happen! (get_expr_mem: var)")
            exit()
        }
        return ptr
    }
    private get_index_expr_mem(this, builder: LLVMBuilderRef, index_exp: IndexExpression): LLVMValueRef {
        let index = this.codegen_expression(builder, index_exp.index)
        let ty = this.codegen_type(index_exp.expression.value_type)
        let ind_ptr: LLVMValueRef = null
        if index_exp.expression.value_type.kind == TypeKind.ARRAY {
            let indices = [$ZERO, index]
            ind_ptr = this.get_expr_mem(builder, index_exp.expression)
            return LLVMBuildInBoundsGEP2(builder, ty, ind_ptr, &indices[0], 2, "arrayidx")
        }
        let indices_1 = [index]
        ind_ptr = this.codegen_expression(builder, index_exp.expression)
        return LLVMBuildInBoundsGEP2(builder, ty, ind_ptr, &indices_1[0], 1, "ptridx")
    }
    private get_member_expr_mem(this, builder: LLVMBuilderRef, member_expr: MemberExpression, ptr: LLVMValueRef): LLVMValueRef {
        let struct_class = this.get_class_from_typeid(member_expr.member_id.type_id)
        let is_function_type = member_expr.value_type.kind == TypeKind.FNC
        let prop_id_key = create_prop_key(member_expr.member_id)
        let class_type = LLVMPointerType(struct_class.llvm_type)
        let method = struct_class.non_vt_methods_map.get(prop_id_key)
        if method != null return method
        if ptr == null {
            ptr = this.codegen_expression(builder, member_expr.expression)
        }
        let indices: LLVMValueRef[] = [] 
        if !is_function_type {
            let vtoffset = struct_class.vtable == null ? 0 : 1
            indices << createLLVMMachineSizeInt(struct_class.fields_pos_map.get(prop_id_key) + vtoffset)
        } else {
            indices << createLLVMMachineSizeInt(0)
            ptr = LLVMBuildInBoundsGEP2(builder, class_type, ptr, indices.buffer, indices.length, "")
            ptr = LLVMBuildLoad2(builder, LLVMPointerType(LLVMGlobalGetValueType(struct_class.vtable)), ptr, "")
            indices[0] = createLLVMMachineSizeInt(struct_class.vt_methods_pos_map.get(prop_id_key))
        }
        return LLVMBuildInBoundsGEP2(builder, class_type, ptr, indices.buffer, indices.length, "memberidx")
    }
    private get_expr_mem(this, builder: LLVMBuilderRef, exp: Expression): LLVMValueRef {
        if exp.expr_kind == ExpressionKind.ACCESS
            return this.get_var_expr_mem(builder, exp as VarAccessExpression)
        if exp.expr_kind == ExpressionKind.INDEX
            return this.get_index_expr_mem(builder, exp as IndexExpression)
        if exp.expr_kind == ExpressionKind.MEMBER
            return this.get_member_expr_mem(builder, exp as MemberExpression, null)
        return this.codegen_expression(builder, exp)
    }
    private store_expr_to_mem(this, builder: LLVMBuilderRef, ptr: LLVMValueRef, expr: Expression): LLVMValueRef {
        if expr.expr_kind == ExpressionKind.ARRAY and expr.is_constant == false
            return this.store_array_elements(builder, ptr, expr as ArrayExpression)
        let value = this.codegen_expression(builder, expr)
        LLVMBuildStore(builder, value, ptr)
        return value
    }
    private codegen_assignment(this, builder: LLVMBuilderRef, assign_exp: BinaryExpression): LLVMValueRef {
        let ptr = this.get_expr_mem(builder, assign_exp.lhs)
        return this.store_expr_to_mem(builder, ptr, assign_exp.rhs)
    }
    private codegen_pow_expr(this, builder: LLVMBuilderRef, ty: Type,  lhs: LLVMValueRef, rhs: LLVMValueRef): LLVMValueRef {
        let args = [lhs, rhs]
        let name = "llvm.pow."+dump_type(ty, true)
        let llvmty = this.codegen_type(ty)
        let cached_instrinsic = this.module_intrinsic_scope.get(name)
        let res: IntrinsicResult
        if cached_instrinsic != null {
            res = cached_instrinsic
        } else {
            res = get_intrinsic(this.module, name, [llvmty, llvmty])
            this.module_intrinsic_scope.set(name, res)
        }
        return LLVMBuildCall2(builder, res.fnc_type, res.call, &args[0], 2, "")
    }
    private codegen_bin_exp(this, builder: LLVMBuilderRef, bin_exp: BinaryExpression): LLVMValueRef {   
        let op = bin_exp.op
        if bin_exp.lhs.value_type != bin_exp.rhs.value_type {
            if op == BinaryOp.EEQ
                return createLLVMBool(false)
            else if op == BinaryOp.NEQ
                return createLLVMBool(true)
        }
        if op == BinaryOp.ASSIGN
            return this.codegen_assignment(builder, bin_exp)
        let lhs = this.codegen_expression(builder, bin_exp.lhs)
        let rhs = this.codegen_expression(builder, bin_exp.rhs)
        let lty = bin_exp.lhs.value_type
        let rty = bin_exp.rhs.value_type
        if is_numeric(rty) and op == BinaryOp.POW
            return this.codegen_pow_expr(builder, rty, lhs, rhs)
        if is_pointer(lty){
            let indices = [op == BinaryOp.ADD ? rhs: neg_int(builder, rhs)]
            let ptr_type = this.codegen_type(lty)
            return LLVMBuildInBoundsGEP2(builder, ptr_type, lhs, &indices[0], 1, "")
        }
        let pred = 0
        let is_const_expr = LLVMIsConstant(lhs) and LLVMIsConstant(rhs)
        if is_int(rty) {
            if op == BinaryOp.ADD {
                return is_const_expr ? LLVMConstAdd(lhs, rhs) : LLVMBuildAdd(builder, lhs, rhs, "")
            } else if op == BinaryOp.SUB {
                return is_const_expr ? LLVMConstSub(lhs, rhs) : LLVMBuildSub(builder, lhs, rhs, "")
            } else if op == BinaryOp.MUL {
                return is_const_expr ? LLVMConstMul(lhs, rhs) : LLVMBuildMul(builder, lhs, rhs, "")
            } else if op == BinaryOp.DIV {
                return LLVMBuildSDiv(builder, lhs, rhs, "")
            } else if op == BinaryOp.MOD {
                return LLVMBuildSRem(builder, lhs, rhs, "")
            } else if op >= BinaryOp.GT and op <= BinaryOp.LTE {
                pred = op - BinaryOp.GT + 38
                return is_const_expr ? LLVMConstICmp(pred, lhs, rhs) : LLVMBuildICmp(builder, pred, lhs, rhs, "")
            } else if op >= BinaryOp.EEQ and op <= BinaryOp.NEQ {
                pred = op - BinaryOp.EEQ + 32
                return is_const_expr ? LLVMConstICmp(pred, lhs, rhs) : LLVMBuildICmp(builder, pred, lhs, rhs, "")
            } else if op == BinaryOp.SL {
                return is_const_expr ? LLVMConstShl(lhs, rhs) : LLVMBuildShl(builder, lhs, rhs, "")
            } else if op == BinaryOp.SR {
                return is_const_expr ? LLVMConstAShr(lhs, rhs) : LLVMBuildAShr(builder, lhs, rhs, "")
            } else if op == BinaryOp.OR {
                return is_const_expr ? LLVMConstOr(lhs, rhs) : LLVMBuildOr(builder, lhs, rhs, "")
            } else if op == BinaryOp.AND {
                return is_const_expr ? LLVMConstAnd(lhs, rhs) : LLVMBuildAnd(builder, lhs, rhs, "")
            } else if op == BinaryOp.XOR {
                return is_const_expr ? LLVMConstXor(lhs, rhs) : LLVMBuildXor(builder, lhs, rhs, "")
            }
        } else if is_float(rty) {
            if op == BinaryOp.ADD { 
                return LLVMBuildFAdd(builder, lhs, rhs, "")
            } else if op == BinaryOp.SUB {
                return LLVMBuildFSub(builder, lhs, rhs, "")
            } else if op == BinaryOp.MUL {
                return LLVMBuildFMul(builder, lhs, rhs, "")
            } else if op == BinaryOp.DIV {
                return LLVMBuildFDiv(builder, lhs, rhs, "")
            } else if op == BinaryOp.MOD {
                return LLVMBuildFRem(builder, lhs, rhs, "")
            } else if op >= BinaryOp.GT and op <= BinaryOp.LTE {
                pred = op - BinaryOp.GT + LLVMRealPredicate.LLVMRealOGT
                return is_const_expr ? LLVMConstFCmp(pred, lhs, rhs) : LLVMBuildFCmp(builder, pred, lhs, rhs, "")
            } else if op >= BinaryOp.EEQ and op <= BinaryOp.NEQ {
                pred = op == BinaryOp.NEQ ? LLVMRealPredicate.LLVMRealONE : LLVMRealPredicate.LLVMRealOEQ
                return is_const_expr ? LLVMConstFCmp(pred, lhs, rhs) : LLVMBuildFCmp(builder, pred, lhs, rhs, "")
            }
        } else if is_bool(rty) {
            if op == BinaryOp.AND {
                return is_const_expr ? LLVMConstAnd(lhs, rhs) : LLVMBuildAnd(builder, lhs, rhs, "")
            } else if op == BinaryOp.OR {
                return is_const_expr ? LLVMConstOr(lhs, rhs) : LLVMBuildOr(builder, lhs, rhs, "")
            }
        }
        return lhs
    }
    private codegen_type_exp(this, builder: LLVMBuilderRef, type_exp: TypeExpression): LLVMValueRef {
        let exp = type_exp.expr
        let bty = type_exp.bound_type
        let val = this.codegen_expression(builder, exp)
        let ty = this.codegen_type(bty)
        let is_const = LLVMIsConstant(val)
        if type_exp.ty_op == TypeExprOp.AS {
            if is_bool(bty) {
                return is_const ? createLLVMBool(!LLVMIsNull(val)) : LLVMBuildIsNotNull(builder, val, "")
            }
            if is_int(exp.value_type) and is_float(bty) {
                return is_const ? LLVMConstSIToFP(val, ty) : LLVMBuildSIToFP(builder, val, ty, "")
            } else if is_float(exp.value_type) and is_int(bty) {
                return is_const ? LLVMConstFPToSI(val, ty) : LLVMBuildFPToSI(builder, val, ty, "")
            } else if is_int(exp.value_type) and is_int(bty){
                return is_const ? LLVMConstIntCast(val, ty, true) : LLVMBuildIntCast2(builder, val, ty, true, "")
            } else if is_float(exp.value_type) and is_float(bty){
                return is_const ? LLVMConstFPCast(val, ty) : LLVMBuildFPCast(builder, val, ty, "")
            }
            return is_const ? LLVMConstBitCast(val, ty) : LLVMBuildBitCast(builder, val, ty, "")
        } 
        return val
    }
    private codegen_unary_exp(this, builder: LLVMBuilderRef, unary_exp: UnaryExpression): LLVMValueRef {
        let op = unary_exp.op
        let exp = unary_exp.expression
        let val = this.codegen_expression(builder, exp)
        if op == UnaryOp.NEG {
            if is_int(exp.value_type){
                return neg_int(builder, val)
            } else if is_float(exp.value_type){
                return LLVMBuildFNeg(builder, val, "")
            }
        } 
        if op == UnaryOp.NOT {
            return LLVMIsConstant(val) ? LLVMConstNot(val) : LLVMBuildNot(builder, val, "")
        }
        if op >= UnaryOp.PREINCR and op <= UnaryOp.POSTDECR or op == UnaryOp.ADDROF {
            let ptr = this.get_expr_mem(builder, exp)
            if op == UnaryOp.ADDROF {
                return ptr
            }
            let alloc_ty = this.codegen_type(exp.value_type)
            let incr_value = op == UnaryOp.PREINCR or op == UnaryOp.POSTINCR ? 1 : -1
            let new_value: LLVMValueRef = null
            if is_pointer(exp.value_type) {
                let indices = [createLLVMMachineSizeInt(incr_value)]
                new_value = LLVMBuildInBoundsGEP2(builder, alloc_ty, val, &indices[0], 1, "incdec.ptr")
            } else if is_numeric(exp.value_type){
                let incr_amt = is_int(exp.value_type) ? LLVMConstInt(alloc_ty, incr_value, false) : LLVMConstReal(alloc_ty, incr_value * 1.0)
                new_value = LLVMBuildAdd(builder, val, incr_amt, "")
            }
            LLVMBuildStore(builder, new_value, ptr)
            return op >= UnaryOp.PREDECR ? val : new_value 
        }
        return val
    }
    private codegen_var_access_exp(this, builder: LLVMBuilderRef, var_exp: VarAccessExpression): LLVMValueRef {
        let val = this.get_var_from_varid(var_exp.value_id)
        let ty = this.codegen_type(var_exp.value_type)
        if val == null {
            println("This should never happen; (codegen_var_access_exp)")
            exit()
        }
        if var_exp.value_id.kind == VarIdKind.FNC
            return val
        if this.in_static_ctx
            return LLVMGetInitializer(val)
        return LLVMBuildLoad2(builder, ty, val, "")
    }
    private codegen_null(null_exp: NullExpression): LLVMValueRef{
        return LLVMConstNull(this.codegen_type(null_exp.value_type))
    }
    private codegen_ternary_exp(this, builder: LLVMBuilderRef, ternary_exp: TernaryExpression): LLVMValueRef {
        let true_block = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "true_block")
        let false_block = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "false_block")
        let end = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "end")
        let cond = this.codegen_expression(builder, ternary_exp.cond)
        LLVMBuildCondBr(builder, cond, true_block, false_block)
        LLVMPositionBuilderAtEnd(builder, true_block)
        let is_true = this.codegen_expression(builder, ternary_exp.is_true)
        LLVMBuildBr(builder, end)
        LLVMPositionBuilderAtEnd(builder, false_block)
        let is_false = this.codegen_expression(builder, ternary_exp.is_false)
        LLVMBuildBr(builder, end)
        LLVMPositionBuilderAtEnd(builder, end)
        let phi_node = LLVMBuildPhi(builder, this.codegen_type(ternary_exp.is_true.value_type), "ternary")
        let incomming_vals = [is_true, is_false]
        let blocks = [true_block, false_block]
        LLVMAddIncoming(phi_node, &incomming_vals[0], &blocks[0], 2)
        return phi_node
    }
    private codegen_call_exp(this, builder: LLVMBuilderRef, call_exp: CallExpression): LLVMValueRef {
        let args: LLVMValueRef[] = []
        let func: LLVMValueRef = null
        let func_ty = this.codegen_type(call_exp.callee.value_type, false)
        if call_exp.callee.expr_kind == ExpressionKind.MEMBER {
            let member_exp = call_exp.callee as MemberExpression
            if member_exp.expression.value_type.kind == TypeKind.OBJECT  { 
                let struct_class = this.get_class_from_typeid(member_exp.member_id.type_id)
                let self = this.codegen_expression(builder, member_exp.expression)
                func = this.codegen_member_exp(builder, member_exp, self)
                args << self
            }
        }
        if args.length == 0 {
            func = this.codegen_expression(builder, call_exp.callee)
        }
        for let i = 0; i < call_exp.args.length; i++ {
            args << this.codegen_expression(builder, call_exp.args[i])
        }
        return LLVMBuildCall2(builder, func_ty, func, args.buffer, args.length, "")
    }
    private codegen_index_exp(this, builder: LLVMBuilderRef, index_exp: IndexExpression): LLVMValueRef {
        let ptr = this.get_expr_mem(builder, index_exp)
        return LLVMBuildLoad2(builder, this.codegen_type(index_exp.value_type), ptr, "")
    }
    private codegen_new_exp(this, builder: LLVMBuilderRef, new_expr: NewExpression): LLVMValueRef {
        let ptr: LLVMValueRef = null
        let llvm_type = this.codegen_type(new_expr.value_type, false)
        if new_expr.onheap
            ptr = LLVMBuildMalloc(builder, llvm_type, "")
        else
            ptr = LLVMBuildAlloca(builder, llvm_type, "")
        if is_object(new_expr.value_type){
            this.build_constructor_call(builder, new_expr.value_type as ObjectType, ptr, new_expr.args)
        }
        return ptr
    }
    private codegen_member_exp(this, builder: LLVMBuilderRef, member_exp: MemberExpression, self: LLVMValueRef): LLVMValueRef {
        let type_kind = member_exp.expression.value_type.kind
        let var_id = (member_exp.expression as VarAccessExpression).value_id
        if type_kind == TypeKind.OBJECT {
            let ptr = this.get_member_expr_mem(builder, member_exp, self)
            return LLVMBuildLoad2(builder, this.codegen_type(member_exp.value_type), ptr, "")
        }
        if var_id.kind == VarIdKind.CLASS {
            let struct_class = this.get_class_from_varid(var_id)
            if struct_class == null {
                println("this should not happen; (codegen_member_exp)")
                exit()
            }
            let prop_id_key = create_prop_key(member_exp.member_id)
            let prop = struct_class.non_vt_methods_map.get(prop_id_key)
            if prop == null {
                println("This should never happen; (codegen_member_expr; prop)")
                exit()
            }
            if member_exp.value_type.kind == TypeKind.FNC
                return prop
            return LLVMBuildLoad2(builder, this.codegen_type(member_exp.value_type), prop, "")
        }
        return null
    }
    private codegen_expression(this, builder: LLVMBuilderRef, expression: Expression): LLVMValueRef {
        if expression.expr_kind == ExpressionKind.BOOL
            return this.codegen_bool(expression as BooleanExpression) /* [] */
        if expression.expr_kind == ExpressionKind.INT
            return this.codegen_int(expression as IntExpression)
        if expression.expr_kind == ExpressionKind.FLOAT
            return this.codegen_float(expression as FloatExpression)
        if expression.expr_kind == ExpressionKind.NULL
            return this.codegen_null(expression as NullExpression)
        if expression.expr_kind == ExpressionKind.STRING
            return this.codegen_string(builder, expression as StringExpression)
        if expression.expr_kind == ExpressionKind.ACCESS 
              return this.codegen_var_access_exp(builder, expression as VarAccessExpression)
        if expression.expr_kind == ExpressionKind.BIN 
            return this.codegen_bin_exp(builder, expression as BinaryExpression)
        if expression.expr_kind == ExpressionKind.UNARY
             return this.codegen_unary_exp(builder, expression as UnaryExpression)
        if expression.expr_kind == ExpressionKind.CALL
            return this.codegen_call_exp(builder, expression as CallExpression)
        if expression.expr_kind == ExpressionKind.INDEX
            return this.codegen_index_exp(builder, expression as IndexExpression)
        if expression.expr_kind == ExpressionKind.MEMBER
            return this.codegen_member_exp(builder, expression as MemberExpression, null)
        if expression.expr_kind == ExpressionKind.NEW
            return this.codegen_new_exp(builder, expression as NewExpression)
        if expression.expr_kind == ExpressionKind.TERNARY
            return this.codegen_ternary_exp(builder, expression as TernaryExpression)
        if expression.expr_kind == ExpressionKind.TYPEBOUND
            return this.codegen_type_exp(builder, expression as TypeExpression)
        if expression.expr_kind == ExpressionKind.ARRAY
            return this.codegen_array_exp(builder, expression as ArrayExpression)
        return LLVMConstInt(LLVMInt32Type(), 0, false)
    }
    private codegen_if_stmt(this, builder: LLVMBuilderRef, if_stmt: IfStatement){
        let condition = this.codegen_expression(builder, if_stmt.condition)
        let if_bb = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "if.entry")
        let else_bb = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "else")
        let merge_bb = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "ifend")
        LLVMBuildCondBr(builder, condition, if_bb, else_bb)
        LLVMPositionBuilderAtEnd(builder, if_bb)
        this.codegen_block(builder, if_stmt.block)
        let if_terminated = is_terminated(if_stmt.block)
        if !if_terminated
            LLVMBuildBr(builder, merge_bb)

        LLVMPositionBuilderAtEnd(builder, else_bb)
        let else_terminated = false
        if if_stmt.else_block != null {
            this.codegen_block(builder, if_stmt.else_block)
            else_terminated = is_terminated(if_stmt.else_block)
        }
        if !else_terminated
            LLVMBuildBr(builder, merge_bb)
        else if if_terminated
            LLVMDeleteBasicBlock(merge_bb)
    
        LLVMPositionBuilderAtEnd(builder, merge_bb)
    }
    private codegen_for_stmt(this, builder: LLVMBuilderRef, for_stmt: ForStatement){
        let for_entry_block = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "for.entry")
        LLVMBuildBr(builder, for_entry_block)
        LLVMPositionBuilderAtEnd(builder, for_entry_block)
        if for_stmt.initialization != null {
            this.codegen_statement(builder, for_stmt.initialization)
        }
        let for_cond_block = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "for.cond")
        let for_body_block = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "for.body")
        let for_incr_block = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "for.incr")
        let for_end_block = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "for.end")
        let prev_break_bb = this.break_bb
        let prev_continue_bb = this.continue_bb
        this.break_bb = for_end_block
        this.continue_bb = for_incr_block
        LLVMBuildBr(builder, for_cond_block)
        LLVMPositionBuilderAtEnd(builder, for_cond_block)
        let condition = createLLVMBool(true)
        if for_stmt.condition != null {
            condition = this.codegen_expression(builder, for_stmt.condition)
        }
        LLVMBuildCondBr(builder, condition, for_body_block, for_end_block)
        LLVMPositionBuilderAtEnd(builder, for_body_block)
        this.codegen_block(builder, for_stmt.block)
        if !is_terminated(for_stmt.block)
            LLVMBuildBr(builder, for_incr_block)
        LLVMPositionBuilderAtEnd(builder, for_incr_block)
        if for_stmt.incr_decr != null
            this.codegen_expression(builder, for_stmt.incr_decr)
        LLVMBuildBr(builder, for_cond_block)
        this.break_bb = prev_break_bb
        this.continue_bb = prev_continue_bb
        LLVMPositionBuilderAtEnd(builder, for_end_block)
    }
    private codegen_while_stmt(this, builder: LLVMBuilderRef, while_stmt: WhileStatement){
        let while_bb = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "while.entry")
        let while_end_bb = LLVMAppendBasicBlockInContext(this.module_ctx, this.function, "while.end")
        let prev_break_bb = this.break_bb
        let prev_continue_bb = this.continue_bb
        this.break_bb = while_end_bb
        this.continue_bb = while_bb
        let condition = this.codegen_expression(builder, while_stmt.condition)
        LLVMBuildCondBr(builder, condition, while_bb, while_end_bb)
        LLVMPositionBuilderAtEnd(builder, while_bb)
        this.codegen_block(builder, while_stmt.block)
        if !is_terminated(while_stmt.block) {
            condition = this.codegen_expression(builder, while_stmt.condition)
            LLVMBuildCondBr(builder, condition, while_bb, while_end_bb)
        }
        LLVMPositionBuilderAtEnd(builder, while_end_bb)
        this.break_bb = prev_break_bb
        this.continue_bb = prev_continue_bb
    }
    private build_constructor_call(this, builder: LLVMBuilderRef, obj_type: ObjectType, self: LLVMValueRef, params: Expression[]){
        let checked_class = get_class(this.program, obj_type.class_id)
        let constructor = this.get_class_from_typeid(obj_type.class_id).constructor
        let constructor_id = checked_class.constructor.id
        let passed_params: LLVMValueRef[] = [self]
        for let i = 0; i < params.length; i++ {
            passed_params << this.codegen_expression(builder, params[i])
        }
        if constructor != null {
            let construct_type = this.codegen_type(checked_class.methods[constructor_id].method_type, false)
            LLVMBuildCall2(builder, construct_type, constructor, passed_params.buffer, passed_params.length, "")
        }
    }
    private codegen_var_declaration(this, builder: LLVMBuilderRef, var_decl: VarDeclaration){
        let var_id = var_decl.var_id
        let var = get_var(this.program, var_id)
        let expression = var.value
        let expression_val_type = var.value_type
        let ty = this.codegen_type(expression_val_type)
        let name = var_decl.name.to_cstring()
        let ptr: LLVMValueRef = null
        if var_decl.is_static {
            let value = this.codegen_expression(builder, expression)
            ptr = LLVMAddGlobal(this.module, ty, name)
            LLVMSetInitializer(ptr, value)
        } else {
            ptr = LLVMBuildAlloca(builder, ty, name)
            if expression != null {
                this.store_expr_to_mem(builder, ptr, expression)
            }
        }
        this.add_var(var_id, ptr)
    }
    private codegen_const_declaration(this, builder: LLVMBuilderRef, const_decl: ConstDeclaration){
        let const_id = const_decl.const_id
        let expression = get_const(this.program, const_id).value
        let ty = this.codegen_type(expression.value_type)
        let name = const_decl.name.to_cstring()
        let ptr: LLVMValueRef = null
        if const_decl.is_static {
            let value = this.codegen_expression(builder, expression)
            ptr = LLVMAddGlobal(this.module, ty, name)
            LLVMSetInitializer(ptr, value)
            LLVMSetGlobalConstant(ptr, true)
        } else {
            ptr = LLVMBuildAlloca(builder, ty, name)
            this.store_expr_to_mem(builder, ptr, expression)
        }
        this.add_var(const_id, ptr)
    }
    private codegen_statement(this, builder: LLVMBuilderRef, statement: Statement){
        if statement.kind == StatementKind.IF
            this.codegen_if_stmt(builder, statement as IfStatement)
        if statement.kind == StatementKind.FOR
            this.codegen_for_stmt(builder, statement as ForStatement)
        if statement.kind == StatementKind.WHILE
            this.codegen_while_stmt(builder, statement as WhileStatement)
        if statement.kind == StatementKind.CONTINUE
            LLVMBuildBr(builder, this.continue_bb)
        if statement.kind == StatementKind.BREAK
            LLVMBuildBr(builder, this.break_bb)
        if statement.kind == StatementKind.VAR
            this.codegen_var_declaration(builder, statement as VarDeclaration)
        if statement.kind == StatementKind.CONST
            this.codegen_const_declaration(builder, statement as ConstDeclaration)
        if statement.kind == StatementKind.RETURN
            this.codegen_return_stmt(builder, statement as ReturnStatement)
        if statement.kind == StatementKind.EXPRESSION
            this.codegen_expression(builder, statement as Expression)
    }
    private codegen_block(this, builder: LLVMBuilderRef, block: Block){
        for let i = 0; i < block.statements.length; ++i {
            this.codegen_statement(builder, block.statements[i])
        }
    }
    private codegen_function(this, func: Function){
        let llvm_fnc = this.get_var_from_varid(func.id)
        if func.is_external { 
            LLVMSetLinkage(llvm_fnc, LLVMLinkage.LLVMExternalLinkage)
            return
        }
        if llvm_fnc == null {
            println("This should never happen; (codegen_function)")
            exit()
        }
        let entry_block: LLVMBasicBlockRef = LLVMAppendBasicBlockInContext(this.module_ctx, llvm_fnc, "")
        let func_builder: LLVMBuilderRef = LLVMCreateBuilderInContext(this.module_ctx)
        LLVMPositionBuilderAtEnd(func_builder, entry_block)
        for let i = 0; i < func.args.length; i++ {
            let arg = func.args[i]
            let param = LLVMGetParam(llvm_fnc, i)
            let alloc = LLVMBuildAlloca(func_builder, this.codegen_type(arg.value_type), arg.name.to_cstring())
            LLVMBuildStore(func_builder, param, alloc)
            this.add_var(arg.id, alloc)
        }
        this.function = llvm_fnc
        this.codegen_block(func_builder, func.block)
        LLVMDisposeBuilder(func_builder)
    }
    private codegen_class(this, checked_class: Class){
        for let i = 0; i < checked_class.methods.length; i++ {
            let struct_class = this.get_class_from_typeid(checked_class.id)
            let method = checked_class.methods[i]
            let prop_id_key = create_prop_key(method.id)
            let llvm_fnc = struct_class.non_vt_methods_map.get(prop_id_key)
            let is_constructor = method.name == "constructor"
            llvm_fnc = LLVMGetNamedFunction(this.module, (checked_class.name + method.name).to_cstring())
            let entry_block = LLVMAppendBasicBlockInContext(this.module_ctx, llvm_fnc, "")
            let func_builder = LLVMCreateBuilderInContext(this.module_ctx)
            LLVMPositionBuilderAtEnd(func_builder, entry_block)
            let offset = 0
            if !method.is_static {
                let object_type = LLVMPointerType(struct_class.llvm_type)
                let this_param = LLVMGetParam(llvm_fnc, 0)
                let this_alloc = LLVMBuildAlloca(func_builder, object_type, "this")
                let this_id = get_scope(this.program, method.scope).vars.get("this")
                LLVMBuildStore(func_builder, this_param, this_alloc)
                if is_constructor and struct_class.vtable != null {
                    let index = [$ZERO]
                    let vt_ptr =  LLVMBuildInBoundsGEP2(func_builder, object_type, this_param, &index[0], 1, "")
                    LLVMBuildStore(func_builder, struct_class.vtable, vt_ptr)
                }
                this.add_var(this_id, this_alloc)
                offset = 1
            }
            for let j = 0; j < method.args.length; j++ {
                let arg = method.args[j]
                let param = LLVMGetParam(llvm_fnc, j + offset)
                let alloc = LLVMBuildAlloca(func_builder, this.codegen_type(arg.value_type), arg.name.to_cstring())
                LLVMBuildStore(func_builder, param, alloc)
                this.add_var(arg.id, alloc)
            }
            this.function = llvm_fnc
            this.codegen_block(func_builder, method.block)
            LLVMDisposeBuilder(func_builder)
        }
    }
    private declare_function(this, func: Function) {
        let llvm_fnc = LLVMAddFunction(this.module, func.name.to_cstring(), this.codegen_type(func.fnc_type, false))
        this.add_var(func.id, llvm_fnc)
    }
    private declare_method(this, method_name: string, method_type: LLVMTypeRef): LLVMValueRef {
        return LLVMAddFunction(this.module, method_name.to_cstring(), method_type)
    }
    private declare_static_props(this, struct_class: StructClass, checked_class: Class){
        struct_class.static_fields_num = 0
        struct_class.static_methods_num = 0
        let buckets = checked_class.static_prop_map.get_buckets()
        for let i = 0; i < buckets.length; i++ {
            let prop_id = buckets[i].value
            let prop_id_key = create_prop_key(prop_id)
            let prop_type = get_class_prop_type(this.program, prop_id)
            let static_prop_name = buckets[i].key
            let static_prop_type = this.codegen_type(prop_type, false)
            let static_prop_name_concat = (checked_class.name + static_prop_name)
            let static_prop_value: LLVMValueRef = null
            if prop_id.kind == PropIdKind.METHOD {
                struct_class.static_methods_num++
                static_prop_value = this.declare_method(static_prop_name_concat, static_prop_type)
            } else {
                struct_class.static_fields_num++
                static_prop_value = LLVMAddGlobal(this.module, static_prop_type, static_prop_name_concat.to_cstring())
                let global_prop_initializer = this.codegen_expression(null, checked_class.fields[prop_id.id].initializer)
                LLVMSetInitializer(static_prop_value, global_prop_initializer)
            }
            struct_class.non_vt_methods_map.set(prop_id_key, static_prop_value)
        }
    }
    private resize_type_array(array: LLVMTypeRef[], amt: int){
        for let i = array.length; i <= amt; i++ {
            array << null
        }
    }
    private resize_value_array(array: LLVMValueRef[], amt: int){
        for let i = array.length; i <= amt; i++ {
            array << null
        }
    }
    private declare_class_props(this, struct_class: StructClass, checked_class: Class, prop_types: LLVMTypeRef[]){
        let buckets = checked_class.prop_map.get_buckets()
        let vtable_values: LLVMValueRef[] = []
        let vtable_types: LLVMTypeRef[] = []
        let field_types: LLVMTypeRef[] = []
        let private_methods_num = 0
        let parent_class: StructClass = null
        if (checked_class.parent != null){
            parent_class = this.get_class_from_typeid((checked_class.parent as ObjectType).class_id)
        }
        for let i = 0; i < buckets.length; i++ {
            let prop_id = buckets[i].value
            let prop_id_key = create_prop_key(prop_id)
            let prop_type = get_class_prop_type(this.program, prop_id)
            let prop_name = buckets[i].key
            let prop_index = prop_id.id
            if prop_id.kind == PropIdKind.FIELD {
                let field_type = this.codegen_type(prop_type)
                if prop_id.type_id == checked_class.id {
                    if prop_index > 0
                        prop_index -= struct_class.static_fields_num
                    if parent_class != null {
                        prop_index += parent_class.fields_num
                    }
                } else {
                    prop_index = parent_class.fields_pos_map.get(prop_id_key)
                }
                struct_class.fields_pos_map.set(prop_id_key, prop_index)
                this.resize_type_array(field_types, prop_index)
                field_types[prop_index] = field_type
            } else {
                let method_type = prop_type as FunctionType
                let llvm_method_val: LLVMValueRef = null
                let llvm_method_type = this.codegen_type(method_type, false)
                if prop_id.type_id == checked_class.id {
                    method_type.arg_types = [new ObjectType(checked_class.id) as Type] + method_type.arg_types
                    llvm_method_type = this.codegen_type(method_type, false)
                    llvm_method_val = this.declare_method((checked_class.name + prop_name), llvm_method_type)
                    if (get_class_prop_visibility(this.program, prop_id) == ACCESS_ATTR.PRIVATE){ 
                        struct_class.non_vt_methods_map.set(prop_id_key, llvm_method_val)
                        private_methods_num++
                        continue
                    }
                    if parent_class != null {
                        prop_index += parent_class.methods_num
                    }
                    if checked_class.constructor != null and prop_index > 0
                        prop_index--
                    if prop_index > 0
                        prop_index -= private_methods_num
                } else {
                    prop_index = parent_class.vt_methods_pos_map.get(prop_id_key)
                    llvm_method_val = LLVMGetNamedFunction(this.module, (get_class(this.program, prop_id.type_id as ClassId).name + prop_name).to_cstring())
                }
                struct_class.vt_methods_pos_map.set(prop_id_key, prop_index)
                this.resize_type_array(vtable_types, prop_index)
                this.resize_value_array(vtable_values, prop_index)
                vtable_values[prop_index] = llvm_method_val
                vtable_types[prop_index] = LLVMPointerType(llvm_method_type)
            }
        }
        struct_class.fields_num = field_types.length
        struct_class.methods_num = vtable_values.length
        if vtable_types.length > 0 {
            let vtable_struct_ty = LLVMStructTypeInContext(this.module_ctx, vtable_types.buffer, vtable_types.length, true)
            struct_class.vtable = LLVMAddGlobal(this.module, vtable_struct_ty, (checked_class.name+"VT").to_cstring())
            let vtable_intializer = LLVMConstStructInContext(this.module_ctx, vtable_values.buffer, vtable_values.length, true)
            LLVMSetInitializer(struct_class.vtable, vtable_intializer)
            prop_types << LLVMPointerType(vtable_struct_ty)
        }
        for i = 0; i < field_types.length; i++ {
            prop_types << field_types[i]
        }
    }
    private declare_class(this, checked_class: Class) {
        let struct_class: StructClass
        let prop_types: LLVMTypeRef[] = []
        struct_class.llvm_type = LLVMStructCreateNamed(this.module_ctx, checked_class.name.to_cstring())
        struct_class.non_vt_methods_map = new Map<string, LLVMValueRef>()
        struct_class.vt_methods_pos_map = new Map<string, int>()
        struct_class.fields_pos_map = new Map<string, int>()
        struct_class.vtable = null
        this.add_class(checked_class.id, struct_class)
        // Taking care of static methods.
        this.declare_static_props(struct_class, checked_class)
        // creating a vtable
        this.declare_class_props(struct_class, checked_class, prop_types)
        LLVMStructSetBody(struct_class.llvm_type, prop_types.buffer, prop_types.length, true)
        // Now constructor
        if checked_class.constructor != null {
            let constructor_type = get_class_prop_type(this.program, checked_class.constructor) as FunctionType
            constructor_type.arg_types = [new ObjectType(checked_class.id) as Type] + constructor_type.arg_types
            let constructor_llvm_method_type = this.codegen_type(constructor_type, false)
            struct_class.constructor = this.declare_method((checked_class.name + "constructor"), constructor_llvm_method_type)
        } else {
            struct_class.constructor = null
        }
    }
}