import panic, FloRange in "utils"
import "typechecker"
import ARCH_WORD_SIZE in "@sys/lib"
import "llvm/types"
import "llvm/core"
import "llvm/debugInfo"
import "types"
import "codegenMod"
class DebugInfoBasicTypes {
    bool_type: LLVMMetadataRef
    uint_type: LLVMMetadataRef
    u4_type: LLVMMetadataRef
    u8_type: LLVMMetadataRef
    u16_type: LLVMMetadataRef
    u32_type: LLVMMetadataRef
    u64_type: LLVMMetadataRef
    u128_type: LLVMMetadataRef
    int_type: LLVMMetadataRef
    i4_type: LLVMMetadataRef
    i8_type: LLVMMetadataRef
    i16_type: LLVMMetadataRef
    i32_type: LLVMMetadataRef
    i64_type: LLVMMetadataRef
    i128_type: LLVMMetadataRef
    f32_type: LLVMMetadataRef
    f64_type: LLVMMetadataRef
    void_type: LLVMMetadataRef
    null_type: LLVMMetadataRef
    constructor(this, builder: LLVMDIBuilderRef){
        this.bool_type = LLVMDIBuilderCreateBasicType(builder, "bool", 4, 1, LLVMDWARFTypeEncoding.DW_ATE_unsigned, LLVMDIFlags.LLVMDIFlagZero)
        this.uint_type = LLVMDIBuilderCreateBasicType(builder, "uint", 4, ARCH_WORD_SIZE, LLVMDWARFTypeEncoding.DW_ATE_unsigned, LLVMDIFlags.LLVMDIFlagZero)
        this.u4_type = LLVMDIBuilderCreateBasicType(builder, "u4", 4, 4, LLVMDWARFTypeEncoding.DW_ATE_unsigned, LLVMDIFlags.LLVMDIFlagZero)
        this.u8_type = LLVMDIBuilderCreateBasicType(builder, "u8", 2, 8, LLVMDWARFTypeEncoding.DW_ATE_unsigned_char, LLVMDIFlags.LLVMDIFlagZero)
        this.u16_type = LLVMDIBuilderCreateBasicType(builder, "u16", 3, 16, LLVMDWARFTypeEncoding.DW_ATE_unsigned, LLVMDIFlags.LLVMDIFlagZero)
        this.u32_type = LLVMDIBuilderCreateBasicType(builder, "u32", 3, 32, LLVMDWARFTypeEncoding.DW_ATE_unsigned, LLVMDIFlags.LLVMDIFlagZero)
        this.u64_type = LLVMDIBuilderCreateBasicType(builder, "u64", 3, 64, LLVMDWARFTypeEncoding.DW_ATE_unsigned, LLVMDIFlags.LLVMDIFlagZero)
        this.u128_type = LLVMDIBuilderCreateBasicType(builder, "u128", 4, 128, LLVMDWARFTypeEncoding.DW_ATE_unsigned, LLVMDIFlags.LLVMDIFlagZero)

        this.int_type = LLVMDIBuilderCreateBasicType(builder, "int", 3, ARCH_WORD_SIZE, LLVMDWARFTypeEncoding.DW_ATE_signed, LLVMDIFlags.LLVMDIFlagZero)
        this.i4_type = LLVMDIBuilderCreateBasicType(builder, "i4", 2, 4, LLVMDWARFTypeEncoding.DW_ATE_signed, LLVMDIFlags.LLVMDIFlagZero)
        this.i8_type = LLVMDIBuilderCreateBasicType(builder, "i8", 2, 8, LLVMDWARFTypeEncoding.DW_ATE_signed, LLVMDIFlags.LLVMDIFlagZero)
        this.i16_type = LLVMDIBuilderCreateBasicType(builder, "i16", 3, 16, LLVMDWARFTypeEncoding.DW_ATE_signed, LLVMDIFlags.LLVMDIFlagZero)
        this.i32_type = LLVMDIBuilderCreateBasicType(builder, "i32", 3, 32, LLVMDWARFTypeEncoding.DW_ATE_signed, LLVMDIFlags.LLVMDIFlagZero)
        this.i64_type = LLVMDIBuilderCreateBasicType(builder, "i64", 3, 64, LLVMDWARFTypeEncoding.DW_ATE_signed, LLVMDIFlags.LLVMDIFlagZero)
        this.i128_type = LLVMDIBuilderCreateBasicType(builder, "i128", 4, 128, LLVMDWARFTypeEncoding.DW_ATE_signed, LLVMDIFlags.LLVMDIFlagZero)

        this.f32_type = LLVMDIBuilderCreateBasicType(builder, "f32", 3, 32, LLVMDWARFTypeEncoding.DW_ATE_float, LLVMDIFlags.LLVMDIFlagZero)
        this.f64_type = LLVMDIBuilderCreateBasicType(builder, "f64", 3, 64, LLVMDWARFTypeEncoding.DW_ATE_float, LLVMDIFlags.LLVMDIFlagZero)
        
        this.void_type = LLVMDIBuilderCreateUnspecifiedType(builder, "void", 4)
        this.null_type = LLVMDIBuilderCreateNullPtrType(builder)
    }
}
class DebugInfo {
    program: Program
    builder: LLVMDIBuilderRef
    CU: LLVMMetadataRef
    files: LLVMMetadataRef[]
    ctx: LLVMContextRef
    lexical_blocks: LLVMMetadataRef[]
    /* Basic Types */
    basic_types: DebugInfoBasicTypes
    constructor(this, program: Program, mod: LLVMModuleRef, fn: String){
        this.program = program
        this.ctx = LLVMGetModuleContext(mod)
        this.builder = LLVMCreateDIBuilder(mod)
        let file = LLVMDIBuilderCreateFile(this.builder, fn.get_bytes(), fn.length as int, "./", 2)
        this.files = [file]
        this.lexical_blocks = []
        this.basic_types = new DebugInfoBasicTypes(this.builder)
        this.CU = LLVMDIBuilderCreateCompileUnit(this.builder, LLVMDWARFSourceLanguage.LLVMDWARFSourceLanguageC, 
            file, "Flo Compiler", 12, false, "", 0, 1, "", 0, LLVMDWARFEmissionKind.LLVMDWARFEmissionFull,
            0, false, true, "/", 1, "", 0 )
    }
    public get_current_file(this): LLVMMetadataRef {
        return LLVMDIScopeGetFile(this.get_current_scope())
    }
    private get_or_create_file(this, id: uint): LLVMMetadataRef {
        if (this.files.length > id) return this.files[id]
        let module = this.program.modules[id]
        if (module == null) panic("module not found in get_file")
        let file = LLVMDIBuilderCreateFile(this.builder, module.name.get_bytes(), module.name.length as int, "./", 2)
        this.files.ensure_capacity(id)
        this.files[id] = file
        return file
    }
    public create_or_get_type(this, tt: Type): LLVMMetadataRef {
         if tt.kind == TypeKind.VOID
            return this.basic_types.void_type
        if tt.kind == TypeKind.BOOL
            return this.basic_types.bool_type
        if is_int(tt)
            return this.get_int_type(tt as IntType)
        if tt.kind == TypeKind.F32 
            return this.basic_types.f32_type
        if tt.kind == TypeKind.F64
            return this.basic_types.f64_type
        if tt.kind == TypeKind.PTR
            return this.create_pointer_type(tt as PointerType)
        if tt.kind == TypeKind.ARRAY
            return this.create_array_type(tt as ArrayType)
        if is_function(tt)
            return this.create_function_type(tt as FunctionType, this.get_current_file())
        return this.basic_types.null_type
    }
    public get_int_type(this, int_type: IntType): LLVMMetadataRef {
        if int_type.kind == TypeKind.I4
            return int_type.is_signed ? this.basic_types.i4_type : this.basic_types.u4_type
        if int_type.kind == TypeKind.I8
            return int_type.is_signed ? this.basic_types.i8_type : this.basic_types.u8_type
        if int_type.kind == TypeKind.I16
            return int_type.is_signed ? this.basic_types.i16_type : this.basic_types.u16_type
        if int_type.kind == TypeKind.I32
            return int_type.is_signed ? this.basic_types.i32_type : this.basic_types.u32_type
        if int_type.kind == TypeKind.INT
            return int_type.is_signed ? this.basic_types.int_type : this.basic_types.uint_type
        if int_type.kind == TypeKind.I64
            return int_type.is_signed ? this.basic_types.i64_type : this.basic_types.u64_type
        else // int_type.kind == TypeKind.I128
            return int_type.is_signed ? this.basic_types.i128_type : this.basic_types.u128_type
    }
    public create_pointer_type(this, pointer_type: PointerType): LLVMMetadataRef {
        let pointee_type = this.create_or_get_type(pointer_type.pointee_type)
        let pointee_name_length = 0
        let point_name = LLVMDITypeGetName(pointee_type, &pointee_name_length)
        let pointer_name = new u8*(pointee_name_length + 1)
        pointer_name.copy_from(point_name, pointee_name_length)
        pointer_name[pointee_name_length] = '*'
        return LLVMDIBuilderCreatePointerType(
                    this.builder, pointee_type,
                    ARCH_WORD_SIZE / 8 as u64, 1, 0,
                    pointer_name, pointee_name_length + 1
                    )
    }
    public get_type_list(this, types: Type[], dest: LLVMMetadataRef*){
        let i = 0
        for atype in types {
            dest[i++] = this.create_or_get_type(atype)
        }
    }
    public create_array_type(this, arr_type: ArrayType): LLVMMetadataRef {
        let element_type = this.create_or_get_type(arr_type.element_type)
        return LLVMDIBuilderCreateArrayType(this.builder, arr_type.length,
                            LLVMDITypeGetSizeInBits(element_type), element_type,
                            &element_type,
                           0)
    }
    public create_function_type(this, fnc_type: FunctionType, file: LLVMMetadataRef): LLVMMetadataRef {
        let params: LLVMMetadataRef*(fnc_type.arg_types.length)
        this.get_type_list(fnc_type.arg_types, params)
        return LLVMDIBuilderCreateSubroutineType(this.builder, file,
                                  params, fnc_type.arg_types.length,
                                  LLVMDIFlags.LLVMDIFlagZero
                                )
    }
    public get_current_scope(this): LLVMMetadataRef {
        if (this.lexical_blocks.length == 0){
            return this.CU
        } else {
            return this.lexical_blocks[this.lexical_blocks.length - 1]
        }
    }
    public create_function(this, func: Function): LLVMMetadataRef {
        let file =  this.get_or_create_file(func.id.module_id)
        return LLVMDIBuilderCreateFunction(this.builder, file, func.name.get_bytes(),
                    func.name.length, "", 0,
                    file, func.definition_range.line, this.create_function_type(func.fnc_type, file),
                    true, true,
                    func.block.range.line, LLVMDIFlags.LLVMDIFlagPrototyped, false)
    } 
    public create_method(this, method: ClassMethod): LLVMMetadataRef {
        let file = this.get_or_create_file(get_class_from_prop_id(this.program, method.id).id.module_id)
        return LLVMDIBuilderCreateFunction(this.builder, file, method.name.get_bytes(),
                    method.name.length, "", 0,
                    file, method.definition_range.line, this.create_function_type(method.method_type, file),
                    true, true,
                    method.block.range.line, LLVMDIFlags.LLVMDIFlagPrototyped, false)
    }
    public emit_location(this, builder: LLVMBuilderRef, expression: Expression){
        if(builder == null) return
        let range = expression.range
        let scope = this.get_current_scope()
        LLVMSetCurrentDebugLocation2(builder, LLVMDIBuilderCreateDebugLocation(this.ctx, range.line, range.col, scope, null))
    }
    public create_lexical_block(this, block: Block): LLVMMetadataRef {
        return LLVMDIBuilderCreateLexicalBlock(this.builder, this.get_current_scope(), this.get_current_file(), block.range.line, block.range.col)
    }
    public create_param_variable(this, dbg_fnc: LLVMMetadataRef, arg_num: uint, arg: FunctionArg): LLVMMetadataRef {
        return LLVMDIBuilderCreateParameterVariable(
            this.builder, dbg_fnc,
            arg.name.get_bytes(), arg.name.length,
            arg_num, this.get_current_file(), arg.range.line, this.create_or_get_type(arg.value_type),
            true, LLVMDIFlags.LLVMDIFlagTypePassByValue
        )
    }
    public create_auto_variable(this, module_id: uint, var_name: String, var_type: Type, var_range: FloRange): LLVMMetadataRef {
        let dbg_type = this.create_or_get_type(var_type)
        return LLVMDIBuilderCreateAutoVariable(
            this.builder, this.get_current_scope(), var_name.get_bytes(), var_name.length,
            this.get_or_create_file(module_id), var_range.line, dbg_type,
            true, LLVMDIFlags.LLVMDIFlagZero, LLVMDITypeGetAlignInBits(dbg_type)
        )
    }
    public insert_declare_before(this, value: LLVMValueRef, dbg_var: LLVMMetadataRef){
        let dbg_expression = LLVMDIBuilderCreateExpression(this.builder, null, 0)
        let scope = LLVMDIVariableGetScope(dbg_var)
        let dbg_loc = LLVMDIBuilderCreateDebugLocation(this.ctx, LLVMDIVariableGetLine(dbg_var), 0, scope, null)
        LLVMDIBuilderInsertDeclareBefore(this.builder, value, dbg_var, dbg_expression, dbg_loc, value)
    }
}
class IntrinsicResult {
    call: LLVMValueRef
    fnc_type: LLVMTypeRef
    constructor(this){
        this.call = null
        this.fnc_type = null
    }
}
class CGClassType  {
    llvm_type: LLVMTypeRef
    vt_methods_pos_map: [String: uint]
    fields_pos_map: [String: uint]
    static_props_map: [String: LLVMValueRef]
    class_constructor: LLVMValueRef
    constructor(this){
        this.vt_methods_pos_map = new [String: uint]()
        this.fields_pos_map = new [String: uint]()
        this.static_props_map = new [String: LLVMValueRef]()
    }
}
class CGEnumType {
    llvm_type: LLVMTypeRef
    field_map: [String: LLVMValueRef]
    metanames: LLVMValueRef
    constructor(this){
        this.field_map = new [String: LLVMValueRef]()
    }
}
fnc get_intrinsic(mod: LLVMModuleRef, name: String, params_types: LLVMTypeRef*, params_length: uint, return_type: LLVMTypeRef, var_args = false): IntrinsicResult {
    let result = new IntrinsicResult()
    result.fnc_type = LLVMFunctionType(return_type, params_types, params_length, var_args)
    let fn_name = name.to_cstring()
    let fnc_in_mod = LLVMGetNamedFunction(mod, fn_name)
    if fnc_in_mod != null {
        result.call = fnc_in_mod
    } else {
        result.call = LLVMAddFunction(mod, fn_name, result.fnc_type)
    }
    return result
}
fnc is_terminated(block: Block): bool {
    return block.returns or block.continues or block.breaks
}
fnc create_var_key(var_id: VarId): String {
    return  "$(var_id.module_id);$(var_id.kind as int);$(var_id.id)"
}
fnc create_type_id_key(type_id: TypeId): String {
    return "$(type_id.module_id);$(type_id.kind as int);$(type_id.id)"
}
fnc create_type_key_from_var(var_id: VarId): String {
    let diff =  (var_id.kind as int) - (VarIdKind.CLASS as int)
    return "$(var_id.module_id);$diff;$(var_id.id)"
}
fnc create_prop_key(prop_id: PropId): String {
    return "$(create_type_id_key(prop_id.type_id));$(prop_id.kind as int);$(prop_id.id)"
}

fnc get_format_from_type(ty: Type): String {
    if is_int(ty){
        let is_signed_int = (ty as IntType).is_signed
        if ty.kind == TypeKind.I128 return is_signed_int ? "%lld" : "%llu"
        if ty.kind == TypeKind.I64 return is_signed_int ? "%ld" : "%lu"
        return is_signed_int ? "%d" : "%u"
    }
    if ty.kind == TypeKind.F32 
        return "%f"
    if ty.kind == TypeKind.F64
        return "%lf"
    if is_pointer(ty)
        return "0x%X"
    return "%s"
}
class CodeGen {
    private module: LLVMModuleRef
    private function: LLVMValueRef
    private break_bb: LLVMBasicBlockRef
    private continue_bb: LLVMBasicBlockRef
    private program: Program
    private in_static_ctx: bool
    private global_var_scope: [String: LLVMValueRef]
    private global_string_scope: [String: LLVMValueRef]
    private global_class_scope: [String: CGClassType]
    private global_enum_scope: [String: CGEnumType]
    private super_functions: [String: LLVMValueRef]
    private debug_info: DebugInfo
    constructor(this, program: Program, module_name: String){
        this.program = program
        this.global_var_scope = new [String: LLVMValueRef]()
        this.global_string_scope = new [String: LLVMValueRef]()
        this.global_class_scope = new [String: CGClassType]()
        this.global_enum_scope = new [String: CGEnumType]()
        this.super_functions = new [String: LLVMValueRef]()
        this.in_static_ctx = true
        this.continue_bb = null
        this.break_bb = null
        this.module = LLVMModuleCreateWithName(module_name.to_cstring())
        LLVMAddModuleFlag(this.module, LLVMModuleFlagBehavior.LLVMModuleFlagBehaviorWarning, "Debug Info Version", 18, LLVMValueAsMetadata(LLVMConstInt(LLVMInt32Type(), LLVMDebugMetadataVersion(), false)))
        this.debug_info = new DebugInfo(this.program, this.module, module_name)
    }
    public codegen(program: Program): GeneratedModule{
        let module = program.modules[2]
        let codegen: CodeGen(program, module.name)
        for enum_ in module.enums  {
            codegen.declare_enum(enum_)
        }
        for class_ in module.classes {
            codegen.declare_class(class_)
        }
        for var_ in module.global_vars {
            codegen.codegen_var_declaration(null, var_)
        }
        for class_ in module.classes {
            codegen.codegen_class(class_)
        }
        for func_ in module.functions {
            codegen.codegen_function(func_)
        }
        codegen.destroy()
        del program
        return new GeneratedModule(codegen.module)
    }
    private destroy(this){
        for var_entry in this.global_var_scope {
            del var_entry.key
        }
        del this.global_var_scope
        for string_entry in this.global_string_scope {
            del string_entry.key
        }
        del this.global_string_scope 
        for func_entry in this.super_functions {
            del func_entry.key
        }
        del this.super_functions
        for class_entry in this.global_class_scope {
            del class_entry.key
            del class_entry.value
        }
        del this.global_class_scope
        for enum_entry in this.global_enum_scope {
            del enum_entry.key
            del enum_entry.value
        }
        del this.global_enum_scope
    }
    private get_var_from_varid(this, var_id: VarId): LLVMValueRef{
        return this.global_var_scope[create_var_key(var_id)]
    }
    private get_class_from_typeid(this, type_id: TypeId): CGClassType {
        return this.global_class_scope[create_type_id_key(type_id)]
    }
    private get_enum_from_typeid(this, type_id: TypeId): CGEnumType {
        return this.global_enum_scope[create_type_id_key(type_id)]
    }
    private add_class(this, type_id: TypeId, struct_class: CGClassType){
        this.global_class_scope[create_type_id_key(type_id)] = struct_class
    }
    private add_enum(this, type_id: TypeId, enum_type: CGEnumType){
        this.global_enum_scope[create_type_id_key(type_id)] = enum_type
    }
    private add_var(this, var_id: VarId, value: LLVMValueRef){
        this.global_var_scope[create_var_key(var_id)] = value
    }
    private neg_int(builder: LLVMBuilderRef, val: LLVMValueRef): LLVMValueRef {
        return LLVMIsConstant(val) ? LLVMConstNeg(val) : LLVMBuildNeg(builder, val, "")
    }
    private createLLVMBool(this, val: bool): LLVMValueRef{
        return LLVMConstInt(LLVMInt1Type(), val ? 1 : 0 as u64, false)
    }
    private createLLVMInt(this, i: uint): LLVMValueRef {
        return LLVMConstInt(LLVMIntType(ARCH_WORD_SIZE), i as u64, false)
    }
    private codegen_type(this, tt: Type, as_pointer = true): LLVMTypeRef {
        if tt.kind == TypeKind.VOID
            return LLVMVoidType()
        if tt.kind == TypeKind.BOOL
            return LLVMInt1Type()
        if tt.kind == TypeKind.I4
            return LLVMIntType(4)
        if tt.kind == TypeKind.I8
            return LLVMInt8Type()
        if tt.kind == TypeKind.I16
            return LLVMInt16Type()
        if tt.kind == TypeKind.I32
            return LLVMInt32Type()
        if tt.kind == TypeKind.INT
            return LLVMIntType(ARCH_WORD_SIZE)
        if tt.kind == TypeKind.I64
            return LLVMInt64Type()
        if tt.kind == TypeKind.I128
            return LLVMInt128Type()
        if tt.kind == TypeKind.F32 
            return LLVMFloatType()
        if tt.kind == TypeKind.F64
            return LLVMDoubleType()
        if tt.kind == TypeKind.PTR
            return LLVMPointerType(this.codegen_type((tt as PointerType).pointee_type))
        if tt.kind == TypeKind.ARRAY {
            let arr_ty = tt as ArrayType
            let element_type = this.codegen_type(arr_ty.element_type)
            return LLVMArrayType(element_type, arr_ty.length)
        }
        if tt.kind == TypeKind.RESOLVED_GENERIC {
            tt = (tt as GenericType).instantiated_type
        }
        if is_function(tt) {
            let func_ty = tt as FunctionType
            let param_types: LLVMTypeRef*(func_ty.arg_types.length)
            let idx = 0
            for arg_ty in func_ty.arg_types {
                param_types[idx++] = this.codegen_type(arg_ty)
            }
            let llvm_fnty = LLVMFunctionType(this.codegen_type(func_ty.return_type), param_types, func_ty.arg_types.length, false)
            return as_pointer ? LLVMPointerType(llvm_fnty) : llvm_fnty
        }
        if tt.kind == TypeKind.TUPLE {
            let tup_ty = tt as TupleType
            let element_types: LLVMTypeRef*(tup_ty.element_types.length)
            let idx = 0
            for element_type in tup_ty.element_types {
                element_types[idx++] = this.codegen_type(element_type)
            }
            return LLVMStructType(element_types, idx, false)
        }
        if tt.kind == TypeKind.OBJECT {
            let obj_type = this.get_or_codegen_class_from_typeid((tt as ObjectType).class_id)
            if obj_type != null {
                return as_pointer ? LLVMPointerType(obj_type.llvm_type) : obj_type.llvm_type
            }
        }
        if tt.kind == TypeKind.ENUM {
            let enum_type = this.get_or_codegen_enum_from_typeid((tt as EnumType).enum_id)
            if enum_type != null {
                return enum_type.llvm_type
            }
        }
        panic("Type could not be code generated")
        return null
    }
    private codegen_return_stmt(this, builder: LLVMBuilderRef, return_stmt: ReturnStatement){
        if return_stmt.return_value != null {
            let expression = this.codegen_expression(builder, return_stmt.return_value)
            if return_stmt.return_value.value_type.kind != TypeKind.VOID {
                LLVMBuildRet(builder, expression)
                return
            }
        }
        LLVMBuildRetVoid(builder)
    }
    private codegen_int(this, int_exp: IntExpression): LLVMValueRef {
        return LLVMConstInt(this.codegen_type(int_exp.value_type), int_exp.value, true)
    }
    private codegen_float(this, float_exp: FloatExpression): LLVMValueRef {
        return LLVMConstReal(this.codegen_type(float_exp.value_type), float_exp.value)
    }
    private codegen_bool(this, bool_exp: BooleanExpression): LLVMValueRef {
        let val = bool_exp.value
        return this.createLLVMBool(val)
    }
    private codegen_atof_call(this, builder: LLVMBuilderRef, arg: LLVMValueRef): LLVMValueRef {
        let arg_types: LLVMTypeRef[1] = [LLVMPointerType(LLVMInt8Type())]
        let args: LLVMValueRef[1] = [arg]
        let float_ty = LLVMDoubleType()
        let atof = get_intrinsic(this.module, "atof", &arg_types[0], arg_types.size, float_ty)
        return LLVMBuildCall2(builder, atof.fnc_type, atof.call, &args[0], args.size, "")
    }
    private codegen_atoi_call(this, builder: LLVMBuilderRef, arg: LLVMValueRef): LLVMValueRef {
        let arg_types: LLVMTypeRef[1] = [LLVMPointerType(LLVMInt8Type())]
        let args: LLVMValueRef[1] = [arg]
        let int_ty = LLVMInt32Type()
        let atoi = get_intrinsic(this.module, "atoi", &arg_types[0], arg_types.size, int_ty)
        return LLVMBuildCall2(builder, atoi.fnc_type, atoi.call, &args[0], args.size, "")
    }
    private codegen_atol_call(this, builder: LLVMBuilderRef, arg: LLVMValueRef): LLVMValueRef {
        let arg_types: LLVMTypeRef[1] = [LLVMPointerType(LLVMInt8Type())]
        let args: LLVMValueRef[1] = [arg]
        let big_int_ty = LLVMInt64Type()
        let atol = get_intrinsic(this.module, "atol", &arg_types[0], arg_types.size, big_int_ty)
        return LLVMBuildCall2(builder, atol.fnc_type, atol.call, &args[0], args.size, "")
    }
    private codegen_realloc_call(this, builder: LLVMBuilderRef, args: LLVMValueRef*): LLVMValueRef {
        let i8_ptr_ty = LLVMPointerType(LLVMInt8Type())
        let arg_types: LLVMTypeRef[2] = [i8_ptr_ty, LLVMIntType(ARCH_WORD_SIZE)]
        let realloc = get_intrinsic(this.module, "realloc", &arg_types[0], arg_types.size, i8_ptr_ty)
        return LLVMBuildCall2(builder, realloc.fnc_type, realloc.call, args, arg_types.size, "")
    }
    private codegen_memcmp_call(this, builder: LLVMBuilderRef, args: LLVMValueRef*): LLVMValueRef {
        let i8_ptr_ty = LLVMPointerType(LLVMInt8Type())
        let int_ty = LLVMIntType(ARCH_WORD_SIZE)
        let arg_types: LLVMTypeRef[3] = [i8_ptr_ty, i8_ptr_ty, int_ty]
        let memcmp = get_intrinsic(this.module, "memcmp", &arg_types[0], arg_types.size, int_ty)
        return LLVMBuildCall2(builder, memcmp.fnc_type, memcmp.call, args, arg_types.size, "")
    }
    private codegen_del_call(this, builder: LLVMBuilderRef, self: LLVMValueRef, obj_ty: ObjectType, range: FloRange): LLVMValueRef {
        let func_ty: FunctionType
        let func = this.codegen_get_member_with_name(builder, self, obj_ty, "__del__", range, &func_ty)
        if func == null return null
        let args: LLVMValueRef[1] = [self]
        return LLVMBuildCall2(builder, this.codegen_type(func_ty, false), func, &args[0], args.size, "")
    }
    private createGlobalLLVMString(this, builder: LLVMBuilderRef, str_value: String): LLVMValueRef {
        let str = this.global_string_scope[str_value]
        if str == null {
            str = LLVMBuildGlobalString(builder, str_value.to_cstring(), "")
            this.global_string_scope[str_value] = str
        }
        return str
    }
    private codegen_safe_fmt_string(this, builder: LLVMBuilderRef, fmt: String, args: LLVMValueRef*, args_length: uint, len_ptr: LLVMValueRef*): LLVMValueRef {
        let i8_ty = LLVMInt8Type()
        let i8_ptr_ty = LLVMPointerType(i8_ty)
        let int_ty = LLVMIntType(ARCH_WORD_SIZE)
        let alloc_buff = LLVMBuildAlloca(builder, i8_ptr_ty, "")
        let llvm_fmt = this.createGlobalLLVMString(builder, fmt)
        let num_passed_args = args_length + 2
        let call_args_buffer: LLVMValueRef*(num_passed_args)
        call_args_buffer[0] = alloc_buff
        call_args_buffer[1] = llvm_fmt
        (call_args_buffer + 2).copy_from(args, args_length)
        let arg_types: LLVMTypeRef[2] = [i8_ptr_ty, i8_ptr_ty]
        let asprintf = get_intrinsic(this.module, "asprintf", &arg_types[0], arg_types.size, int_ty, true)
        let str_len = LLVMBuildCall2(builder, asprintf.fnc_type, asprintf.call, call_args_buffer, num_passed_args, "")
        if len_ptr != null {
            len_ptr[0] = str_len
        }
        return LLVMBuildLoad2(builder, i8_ptr_ty, alloc_buff, "")
    }
    private get_string_val(this, builder: LLVMBuilderRef, llvm_val: LLVMValueRef, val_ty: Type, malloc_list: LLVMValueRef[], range: FloRange, depth = 0): LLVMValueRef {
        if is_bool(val_ty){
            let true_str = this.createGlobalLLVMString(builder, "true")
            let false_str = this.createGlobalLLVMString(builder, "false")
            return LLVMBuildSelect(builder, llvm_val, true_str, false_str, "")
        }
        if is_object(val_ty){
            if is_buitin(this.program, val_ty, "String") {
                let result = this.codegen_string_to_cstring(builder, llvm_val, range)
                malloc_list << result
                return result
            }
            //TODO: will need to check for nullable
            let obj_class = get_class_from_type(this.program, val_ty)
            let string_value = String.with_capacity(obj_class.name.length + 14)
            string_value += "$(obj_class.name) {\n"
            if depth == 3 {
                string_value += " ... }"
                return this.createGlobalLLVMString(builder, string_value)
            }
            let fmt_args_length = obj_class.prop_map.length
            let fmt_args: LLVMValueRef*(fmt_args_length)
            let idx = 0
            for field_entry in obj_class.prop_map {
                let field_ty = get_class_prop_type(this.program, field_entry.value)
                let member_val = this.codegen_get_member_with_name(builder, llvm_val, val_ty as ObjectType, field_entry.key, range)
                let arg = this.get_string_val(builder, member_val, field_ty, malloc_list, range, depth + 1)
                if !is_object(field_ty) and LLVMGetTypeKind(LLVMTypeOf(arg)) == LLVMTypeKind.LLVMPointerTypeKind and !LLVMIsGlobalConstant(arg) {
                    malloc_list << arg
                }
                fmt_args[idx++] = arg
                string_value += " '$(field_entry.key)': $(get_format_from_type(field_ty)), \n"
            }
            string_value +="}"
            let result = this.codegen_safe_fmt_string(builder, string_value, fmt_args, fmt_args_length, null)
            malloc_list << result
            return result
        }
        //TODO: Better toString for functions
        if is_function(val_ty) {
            return this.createGlobalLLVMString(builder, "<function>")
        }
        if val_ty.kind == TypeKind.ENUM {
            let enum_meta = this.get_or_codegen_enum_from_typeid((val_ty as EnumType).enum_id).metanames
            let indices: LLVMValueRef[1] = [llvm_val]
            let ptr = LLVMBuildInBoundsGEP2(builder, LLVMTypeOf(enum_meta), enum_meta, &indices[0], indices.size, "")
            return LLVMBuildLoad2(builder, LLVMTypeOf(ptr), ptr, "")
        }
        return llvm_val
    }
    private codegen_string(this, builder: LLVMBuilderRef, string_exp: StringExpression): LLVMValueRef {
        let str_value = string_exp.value
        let fmt_string = String.with_capacity(str_value.length + string_exp.fmt_indices.length*2)
        let chars = str_value.get_bytes()
        let curr_ind = 0
        let prev_ind = 0
        let malloc_list: LLVMValueRef[] = []
        let call_args_length = string_exp.template_args.length
        let call_args: LLVMValueRef*(call_args_length)
        for elem in 0..call_args_length {
            let i = elem as uint
            let templ_arg = string_exp.template_args[i]
            let templ_arg_ty = templ_arg.value_type
            let fmt = get_format_from_type(templ_arg_ty)
            prev_ind = curr_ind
            curr_ind = string_exp.fmt_indices[i]
            let incr = curr_ind - prev_ind
            fmt_string.append_bytes_length(chars, incr)
            fmt_string += fmt
            chars += incr
            let templ_arg_exp = this.codegen_expression(builder, templ_arg)
            call_args[i] = this.get_string_val(builder, templ_arg_exp, templ_arg_ty, malloc_list, string_exp.range)
        }
        fmt_string.append_bytes_length(chars, str_value.length - curr_ind)
        str_value = fmt_string
        let str_len = this.createLLVMInt(str_value.length)
        let str = call_args_length > 0 ? this.codegen_safe_fmt_string(builder, str_value, call_args, call_args_length, &str_len) : this.createGlobalLLVMString(builder, str_value)
        if is_object(string_exp.value_type) {
            let str_ty = this.codegen_type(string_exp.value_type, false)
            if LLVMIsConstant(str) and LLVMIsGlobalConstant(str) {
                let func = LLVMGetNamedFunction(this.module, "String_from_bytes")
                if func == null {
                    panic("Could not find function 'String.from_bytes' in program")
                }
                let args: LLVMValueRef[2] = [str, str_len]
                str = LLVMBuildCall2(builder, LLVM_GetFunctionType(func), func, &args[0], args.size, "")
            } else {
                let self = LLVMBuildMalloc(builder, str_ty, "")
                let args: LLVMValueRef[3] = [self, str, str_len] 
                this.build_constructor_call(builder, string_exp.value_type as ObjectType, &args[0], args.size)
                str = self
            }
        }
        for mem in malloc_list {
            LLVMBuildFree(builder, mem)
        }
        del malloc_list
        return str
    }
    private store_array_elements(this, builder: LLVMBuilderRef, ptr: LLVMValueRef, array_exp: ArrayExpression): LLVMValueRef {
        let next_args: LLVMValueRef[1] = [this.createLLVMInt(1)]
        let ptr_ty = LLVMTypeOf(ptr)
        let addr = ptr
        for value in array_exp.elements {
            LLVMBuildStore(builder, this.codegen_expression(builder, value), addr)
            addr = LLVMBuildInBoundsGEP2(builder, ptr_ty, addr, &next_args[0], next_args.size, "")
        }
        return addr
    }
    private store_tuple_elements(this, builder: LLVMBuilderRef, ptr: LLVMValueRef, tuple_exp: TupleExpression): LLVMValueRef {
        let int32ty = LLVMInt32Type()
        let next_args: LLVMValueRef[2] = [LLVMConstInt(int32ty, 0, false), null]
        let idx: u64 = 0
        let tuple_type = this.codegen_type(tuple_exp.value_type)
        for value in tuple_exp.elements {
            next_args[1] = LLVMConstInt(int32ty, idx++, false)
            let addr = LLVMBuildInBoundsGEP2(builder, tuple_type, ptr, &next_args[0], next_args.size, "")
            LLVMBuildStore(builder, this.codegen_expression(builder, value), addr)
        }
        return ptr
    }
    private codegen_array_exp(this, builder: LLVMBuilderRef, array_exp: ArrayExpression): LLVMValueRef {
        if array_exp.value_type.kind == TypeKind.RESOLVED_GENERIC {
            return this.codegen_builtin_array(builder, array_exp.value_type as GenericType, array_exp)
        } 
        //FIXME: What if array is not constant?
        if !array_exp.is_constant {
            panic("Array expression is not a constant (codegen_array_exp)")
        }
        let elem_ty = this.codegen_type((array_exp.value_type as ArrayType).element_type)
        let elems_buffer: LLVMValueRef*(array_exp.elements.length)
        let idx = 0
        for value in array_exp.elements {
            elems_buffer[idx++] = this.codegen_expression(builder, value)
        }
        return LLVMConstArray(elem_ty, elems_buffer, array_exp.elements.length)
    }
    private codegen_tuple_exp(this, builder: LLVMBuilderRef, tuple_exp: TupleExpression): LLVMValueRef {
        //FIXME: What if tuple is not constant?
        if !tuple_exp.is_constant {
            panic("Array expression is not a constant (codegen_tuple_exp)")
        }
        let elems_buffer: LLVMValueRef*(tuple_exp.elements.length)
        let idx = 0
        for value in tuple_exp.elements {
            elems_buffer[idx++] = this.codegen_expression(builder, value)
        }
        return LLVMConstStruct(elems_buffer, idx, false)
    }
    private get_or_codegen_var(this, target_id: VarId): LLVMValueRef {
        let var = this.get_var_from_varid(target_id)
        if var != null return var
        // We need to find this var..
        let module = this.program.modules[target_id.module_id]
        if target_id.kind == VarIdKind.VAR or target_id.kind == VarIdKind.CONST {
            for global_var in module.global_vars {
                let var_id = global_var.var_id
                if var_id.id == target_id.id {
                    this.codegen_var_declaration(null, global_var)
                    return this.get_var_from_varid(target_id)
                }
            }
        }
        if target_id.kind == VarIdKind.FNC {
            for function in module.functions {
                let fnc_id = function.id
                if fnc_id.id == target_id.id {
                    this.codegen_function(function)
                    return this.get_var_from_varid(target_id)
                }
            }
        }
        return null
    }
    private construct_class(this, class_: Class): CGClassType {
        this.declare_class(class_)
        this.codegen_class(class_)
        return this.get_class_from_typeid(class_.id)
    }
    private construct_enum(this, enum_: Enum): CGEnumType {
        this.declare_enum(enum_)
        return this.get_enum_from_typeid(enum_.id)
    }
    private get_or_codegen_class_from_typeid(this, ty_id: TypeId): CGClassType {
        let cg_class = this.get_class_from_typeid(ty_id)
        if cg_class != null return cg_class
        let class_ = get_class(this.program, ty_id as ClassId)
        return this.construct_class(class_)
    }
    private get_or_codegen_enum_from_typeid(this, ty_id: TypeId): CGEnumType {
        let cg_enum = this.get_enum_from_typeid(ty_id)
        if cg_enum != null return cg_enum
        let enum_ = get_enum(this.program, ty_id as EnumId)
        return this.construct_enum(enum_)
    }
    private get_var_expr_mem(this, builder: LLVMBuilderRef, var_exp: VarAccessExpression): LLVMValueRef {
        let ptr = this.get_or_codegen_var(var_exp.value_id)
        if ptr == null {
            panic("This should never happen! (get_expr_mem: var)")
        }
        return ptr
    }
    private get_index_expr_mem(this, builder: LLVMBuilderRef, index_exp: IndexExpression): LLVMValueRef {
        let index = this.codegen_expression(builder, index_exp.index)
        let expr_val_ty = index_exp.expression.value_type
        let ty = is_pointer(expr_val_ty) ? this.codegen_type((expr_val_ty as PointerType).pointee_type) : this.codegen_type(expr_val_ty)
        let ind_ptr: LLVMValueRef = null
        if expr_val_ty.kind == TypeKind.ARRAY {
            let indices: LLVMValueRef[2] = [this.createLLVMInt(0), index]
            ind_ptr = this.get_expr_mem(builder, index_exp.expression)
            return LLVMBuildInBoundsGEP2(builder, ty, ind_ptr, &indices[0], indices.size, "")
        }
        if expr_val_ty.kind == TypeKind.TUPLE {
            let int32ty = LLVMInt32Type()
            let indices: LLVMValueRef[2] = [LLVMConstInt(int32ty, 0, false), LLVMConstIntCast(index, int32ty, false)]
            ind_ptr = this.get_expr_mem(builder, index_exp.expression)
            return LLVMBuildInBoundsGEP2(builder, ty, ind_ptr, &indices[0], indices.size, "")
        }
        let indices_1: LLVMValueRef[1] = [index]
        ind_ptr = this.codegen_expression(builder, index_exp.expression)
        return LLVMBuildInBoundsGEP2(builder, ty, ind_ptr, &indices_1[0], indices_1.size, "")
    }
    private get_member_expr_mem(this, builder: LLVMBuilderRef, member_expr: MemberExpression, ptr: LLVMValueRef): LLVMValueRef {
        let struct_class = this.get_or_codegen_class_from_typeid(member_expr.member_id.type_id)
        let prop_id_key = create_prop_key(member_expr.member_id)
        let struct_type = struct_class.llvm_type
        let method = struct_class.static_props_map[prop_id_key]
        if method != null return method
        if ptr == null {
            ptr = this.codegen_expression(builder, member_expr.expression)
        }
        let indices_buffer: LLVMValueRef[2] = [LLVMConstInt(LLVMInt32Type(), 0, false), null]
        indices_buffer[1] = LLVMConstInt(LLVMInt32Type(), struct_class.fields_pos_map[prop_id_key] as u64, false)
        return LLVMBuildInBoundsGEP2(builder, struct_type, ptr, &indices_buffer[0], indices_buffer.size, "memberidx")
    }
    private get_expr_mem(this, builder: LLVMBuilderRef, exp: Expression): LLVMValueRef {
        if exp.expr_kind == ExpressionKind.ACCESS
            return this.get_var_expr_mem(builder, exp as VarAccessExpression)
        if exp.expr_kind == ExpressionKind.INDEX
            return this.get_index_expr_mem(builder, exp as IndexExpression)
        if exp.expr_kind == ExpressionKind.MEMBER
            return this.get_member_expr_mem(builder, exp as MemberExpression, null)
        return this.codegen_expression(builder, exp)
    }
    // Only call for non-static members.
    private codegen_get_member_mem_with_name(this, builder: LLVMBuilderRef, self: LLVMValueRef, obj_type: ObjectType, name: String, range: FloRange, save_type: Type* = null): LLVMValueRef {
        let obj_class = get_class_from_type(this.program, obj_type)
        let prop_id = obj_class.prop_map[name]
        if prop_id == null  return null
        let prop_ty = get_class_prop_type(this.program, prop_id)
        if save_type != null save_type[0] = prop_ty
        let expr: Expression(ExpressionKind.ACCESS, obj_type, range)
        let member_expr: MemberExpression(expr, prop_id, prop_ty, range)
        return this.get_member_expr_mem(builder, member_expr, self)
    }
    // Only call for non-static members.
    private codegen_get_member_with_name(this, builder: LLVMBuilderRef, self: LLVMValueRef, obj_type: ObjectType, name: String, range: FloRange, save_type: Type* = null): LLVMValueRef {
        let obj_class = get_class_from_type(this.program, obj_type)
        let prop_id = obj_class.prop_map[name]
        if prop_id == null  return null
        let prop_ty = get_class_prop_type(this.program, prop_id)
        if save_type != null save_type[0] = prop_ty
        let expr: Expression(ExpressionKind.ACCESS, obj_type, range)
        let member_expr: MemberExpression(expr, prop_id, prop_ty, range)
        return this.codegen_member_exp(builder, member_expr, self)
    }
    private codegen_builtin_array(this, builder: LLVMBuilderRef, arrgen_ty: GenericType, arr_expr: ArrayExpression): LLVMValueRef {
        let obj_ty = (arrgen_ty as GenericType).instantiated_type as ObjectType
        this.debug_info.emit_location(builder, arr_expr)
        let objllvm_ty = this.codegen_type(obj_ty, false)
        let elmllvm_ty = this.codegen_type(arrgen_ty.arg_types[0])
        let self = LLVMBuildMalloc(builder, objllvm_ty, "")
        let len_val = arr_expr.elements.length
        let size_value = this.createLLVMInt(len_val > 0 ? len_val: 1)
        let len_value = this.createLLVMInt(len_val)
        let args: LLVMValueRef[2] = [self, LLVMConstMul(size_value, LLVMSizeOf(elmllvm_ty))]
        this.build_constructor_call(builder, obj_ty, &args[0], args.size)
        let arg_ptr = this.codegen_get_member_with_name(builder, self, obj_ty, "buffer", arr_expr.range)
        this.store_array_elements(builder, arg_ptr, arr_expr)
        let len_ptr = this.codegen_get_member_mem_with_name(builder, self, obj_ty, "length", arr_expr.range)
        LLVMBuildStore(builder, len_value, len_ptr)
        return self
    }
    private store_expr_to_mem(this, builder: LLVMBuilderRef, ptr: LLVMValueRef, expr: Expression): LLVMValueRef {
        let value: LLVMValueRef = null
        if expr.expr_kind == ExpressionKind.ARRAY and !expr.is_constant {
            let arr_expr = expr as ArrayExpression
            let value_ty = expr.value_type
            if value_ty.kind != TypeKind.RESOLVED_GENERIC {
                let zero = this.createLLVMInt(0)
                let args: LLVMValueRef[2] = [zero, zero]
                let array_type = this.codegen_type(value_ty)
                ptr = LLVMBuildInBoundsGEP2(builder, array_type, ptr, &args[0], args.size, "")
                return this.store_array_elements(builder, ptr, arr_expr)
            }
            value = this.codegen_builtin_array(builder, value_ty as GenericType, arr_expr)
        } else if expr.expr_kind == ExpressionKind.TUPLE and !expr.is_constant {
            return this.store_tuple_elements(builder, ptr, expr as TupleExpression)
        } else {
            value = this.codegen_expression(builder, expr)
        }
        LLVMBuildStore(builder, value, ptr)
        return value
    }
    private codegen_assignment(this, builder: LLVMBuilderRef, assign_exp: BinaryExpression): LLVMValueRef {
        let ptr = this.get_expr_mem(builder, assign_exp.lhs)
        return this.store_expr_to_mem(builder, ptr, assign_exp.rhs)
    }
    private codegen_pow_expr(this, builder: LLVMBuilderRef, ty: Type,  lhs: LLVMValueRef, rhs: LLVMValueRef): LLVMValueRef {
        let args: LLVMValueRef[2] = [lhs, rhs]
        let name = "llvm.pow.$(dump_type(ty, true))"
        let llvmty = this.codegen_type(ty)
        let arg_types: LLVMTypeRef[2] = [llvmty, llvmty]
        let res = get_intrinsic(this.module, name, &arg_types[0], arg_types.size, llvmty)
        return LLVMBuildCall2(builder, res.fnc_type, res.call, &args[0], args.size, "")
    }
    private codegen_bin_exp(this, builder: LLVMBuilderRef, bin_exp: BinaryExpression): LLVMValueRef {   
        let op = bin_exp.op
        if bin_exp.lhs.value_type.kind != bin_exp.rhs.value_type.kind {
            if op == BinaryOp.EEQ
                return this.createLLVMBool(false)
            else if op == BinaryOp.NEQ
                return this.createLLVMBool(true)
        }
        if op == BinaryOp.ASSIGN
            return this.codegen_assignment(builder, bin_exp)
        let lhs = this.codegen_expression(builder, bin_exp.lhs)
        let rhs = this.codegen_expression(builder, bin_exp.rhs)
        if is_null_expression(bin_exp.rhs) and (op == BinaryOp.EEQ or op == BinaryOp.NEQ) {
            if LLVMIsConstant(lhs)
                return this.createLLVMBool(op == BinaryOp.EEQ ? LLVMIsNull(lhs) : LLVMIsNull(lhs))
            return op == BinaryOp.EEQ ? LLVMBuildIsNull(builder, lhs, "") : LLVMBuildIsNotNull(builder, lhs, "")
        }
        let lty = bin_exp.lhs.value_type
        let rty = bin_exp.rhs.value_type
        if is_numeric(rty) and op == BinaryOp.POW
            return this.codegen_pow_expr(builder, rty, lhs, rhs)
        if is_pointer(lty){
            let indices: LLVMValueRef[1] = [op == BinaryOp.ADD ? rhs: CodeGen.neg_int(builder, rhs)]
            let pointee_type = this.codegen_type((lty as PointerType).pointee_type)
            return LLVMBuildInBoundsGEP2(builder, pointee_type, lhs, &indices[0], indices.size, "")
        }
        let ipred: LLVMIntPredicate = null
        let rpred: LLVMRealPredicate = null
        let is_const_expr = LLVMIsConstant(lhs) and LLVMIsConstant(rhs)
        if is_int(rty) or (is_object(lty) and is_object(rty) and (op == BinaryOp.EEQ or op == BinaryOp.NEQ)) or (rty.kind == TypeKind.ENUM) {
            if op == BinaryOp.ADD {
                if ((bin_exp.value_type as IntType).is_signed)
                    return is_const_expr ? LLVMConstNSWAdd(lhs, rhs) : LLVMBuildNSWAdd(builder, lhs, rhs, "")
                return is_const_expr ? LLVMConstNUWAdd(lhs, rhs) : LLVMBuildNUWAdd(builder, lhs, rhs, "")
            } else if op == BinaryOp.SUB {
                if ((bin_exp.value_type as IntType).is_signed)
                    return is_const_expr ? LLVMConstNSWSub(lhs, rhs) : LLVMBuildNSWSub(builder, lhs, rhs, "")
                return is_const_expr ? LLVMConstNUWSub(lhs, rhs) : LLVMBuildNUWSub(builder, lhs, rhs, "")
            } else if op == BinaryOp.MUL {
                if ((bin_exp.value_type as IntType).is_signed)
                    return is_const_expr ? LLVMConstNSWMul(lhs, rhs) : LLVMBuildNSWMul(builder, lhs, rhs, "")
                return is_const_expr ? LLVMConstNUWMul(lhs, rhs) : LLVMBuildNUWMul(builder, lhs, rhs, "")
            } else if op == BinaryOp.DIV {
                if ((bin_exp.value_type as IntType).is_signed)
                        return LLVMBuildSDiv(builder, lhs, rhs, "")
                    return LLVMBuildUDiv(builder, lhs, rhs, "")
            } else if op == BinaryOp.MOD {
                if ((bin_exp.value_type as IntType).is_signed)
                    return LLVMBuildSRem(builder, lhs, rhs, "")
                return LLVMBuildURem(builder, lhs, rhs, "")
            } else if op >= BinaryOp.GT and op <= BinaryOp.LTE {
                let offset = (rty as IntType).is_signed ? 38 : 34
                ipred = ((op as int) - (BinaryOp.GT as int) + 38) as LLVMIntPredicate // Consider Enum variants
                return is_const_expr ? LLVMConstICmp(ipred, lhs, rhs) : LLVMBuildICmp(builder, ipred, lhs, rhs, "")
            } else if op >= BinaryOp.EEQ and op <= BinaryOp.NEQ {
                ipred = ((op as int) - (BinaryOp.EEQ as int) + 32) as LLVMIntPredicate
                return is_const_expr ? LLVMConstICmp(ipred, lhs, rhs) : LLVMBuildICmp(builder, ipred, lhs, rhs, "")
            } else if op == BinaryOp.SL {
                return is_const_expr ? LLVMConstShl(lhs, rhs) : LLVMBuildShl(builder, lhs, rhs, "")
            } else if op == BinaryOp.SR {
                return is_const_expr ? LLVMConstLShr(lhs, rhs) : LLVMBuildLShr(builder, lhs, rhs, "")
            } else if op == BinaryOp.OR {
                return is_const_expr ? LLVMConstOr(lhs, rhs) : LLVMBuildOr(builder, lhs, rhs, "")
            } else if op == BinaryOp.AND {
                return is_const_expr ? LLVMConstAnd(lhs, rhs) : LLVMBuildAnd(builder, lhs, rhs, "")
            } else if op == BinaryOp.XOR {
                return is_const_expr ? LLVMConstXor(lhs, rhs) : LLVMBuildXor(builder, lhs, rhs, "")
            }
        } else if is_float(rty) {
            if op == BinaryOp.ADD { 
                return LLVMBuildFAdd(builder, lhs, rhs, "")
            } else if op == BinaryOp.SUB {
                return LLVMBuildFSub(builder, lhs, rhs, "")
            } else if op == BinaryOp.MUL {
                return LLVMBuildFMul(builder, lhs, rhs, "")
            } else if op == BinaryOp.DIV {
                return LLVMBuildFDiv(builder, lhs, rhs, "")
            } else if op == BinaryOp.MOD {
                return LLVMBuildFRem(builder, lhs, rhs, "")
            } else if op >= BinaryOp.GT and op <= BinaryOp.LTE {
                rpred = ((op as int) - (BinaryOp.GT as int) + (LLVMRealPredicate.LLVMRealOGT as int)) as LLVMRealPredicate
                return is_const_expr ? LLVMConstFCmp(rpred, lhs, rhs) : LLVMBuildFCmp(builder, rpred, lhs, rhs, "")
            } else if op >= BinaryOp.EEQ and op <= BinaryOp.NEQ {
                rpred = op == BinaryOp.NEQ ? LLVMRealPredicate.LLVMRealONE : LLVMRealPredicate.LLVMRealOEQ
                return is_const_expr ? LLVMConstFCmp(rpred, lhs, rhs) : LLVMBuildFCmp(builder, rpred, lhs, rhs, "")
            }
        } else if is_bool(rty) {
            if op == BinaryOp.AND or op == BinaryOp.EEQ {
                return is_const_expr ? LLVMConstAnd(lhs, rhs) : LLVMBuildAnd(builder, lhs, rhs, "")
            } else if op == BinaryOp.OR {
                return is_const_expr ? LLVMConstOr(lhs, rhs) : LLVMBuildOr(builder, lhs, rhs, "")
            } else if op == BinaryOp.NEQ {
                return is_const_expr ? LLVMConstXor(lhs, rhs) : LLVMBuildXor(builder, lhs, rhs, "")
            }
        }
        panic("Unknown bin expression case case!")
        return lhs
    }
    private codegen_typecast_exp(this, builder: LLVMBuilderRef, type_exp: TypeCastExpression): LLVMValueRef {
        let exp = type_exp.expr
        let value_type = type_exp.value_type
        let val = this.codegen_expression(builder, exp)
        let ty = this.codegen_type(value_type)
        let is_const = LLVMIsConstant(val)
        if is_bool(value_type) {
            return is_const ? this.createLLVMBool(!LLVMIsNull(val)) : LLVMBuildIsNotNull(builder, val, "")
        }
        if is_int(exp.value_type) and is_float(value_type) {
            return is_const ? LLVMConstSIToFP(val, ty) : LLVMBuildSIToFP(builder, val, ty, "")
        } else if is_float(exp.value_type) and is_int(value_type) {
            return is_const ? LLVMConstFPToSI(val, ty) : LLVMBuildFPToSI(builder, val, ty, "")
        } else if is_int(exp.value_type) and is_int(value_type){ // FIXME: Enums?
            let int_ty = value_type as IntType
            return is_const ? LLVMConstIntCast(val, ty, int_ty.is_signed) : LLVMBuildIntCast2(builder, val, ty, int_ty.is_signed, "")
        } else if is_float(exp.value_type) and is_float(value_type){
            return is_const ? LLVMConstFPCast(val, ty) : LLVMBuildFPCast(builder, val, ty, "")
        }
        return is_const ? LLVMConstBitCast(val, ty) : LLVMBuildBitCast(builder, val, ty, "")
    }
    //TODO: Refactor!!
    private codegen_unary_exp(this, builder: LLVMBuilderRef, unary_exp: UnaryExpression): LLVMValueRef { 
        let op = unary_exp.op
        let exp = unary_exp.expression
        let val = this.codegen_expression(builder, exp)
        if op == UnaryOp.DEL {
            if is_object(exp.value_type)
                this.codegen_del_call(builder, val, exp.value_type as ObjectType, unary_exp.range)
            LLVMBuildFree(builder, val)
            return val
        }
        if op == UnaryOp.NEG {
            if is_int(exp.value_type){
                if (exp.value_type as IntType).is_signed
                    return LLVMIsConstant(val) ? LLVMConstNSWNeg(val) : LLVMBuildNSWNeg(builder, val, "")
                return LLVMIsConstant(val) ? LLVMConstNUWNeg(val) : LLVMBuildNUWNeg(builder, val, "")
            } else if is_float(exp.value_type){
                return LLVMBuildFNeg(builder, val, "")
            }
        } 
        if op == UnaryOp.NOT {
            return LLVMIsConstant(val) ? LLVMConstNot(val) : LLVMBuildNot(builder, val, "")
        }
        if op >= UnaryOp.PREINCR and op <= UnaryOp.POSTDECR or op == UnaryOp.ADDROF {
            let ptr = this.get_expr_mem(builder, exp)
            if op == UnaryOp.ADDROF {
                return ptr
            }
            let alloc_ty = this.codegen_type(exp.value_type)
            let incr_value: i64 = op == UnaryOp.PREINCR or op == UnaryOp.POSTINCR ? 1 : -1
            let new_value: LLVMValueRef = null
            if is_pointer(exp.value_type) {
                alloc_ty = this.codegen_type((exp.value_type as PointerType).pointee_type)
                let indices: LLVMValueRef[1] = [LLVMConstInt(LLVMInt32Type(), incr_value as u64, true)]
                new_value = LLVMBuildInBoundsGEP2(builder, alloc_ty, val, &indices[0], indices.size, "incdec.ptr")
            } else if is_numeric(exp.value_type){
                let incr_amt = is_int(exp.value_type) ? LLVMConstInt(alloc_ty, incr_value as u64, false) : LLVMConstReal(alloc_ty, incr_value * 1.0 as f64)
                new_value = LLVMBuildNSWAdd(builder, val, incr_amt, "")
            }
            LLVMBuildStore(builder, new_value, ptr)
            return op >= UnaryOp.PREDECR ? val : new_value 
        }
        return val
    }
    private codegen_var_access_exp(this, builder: LLVMBuilderRef, var_exp: VarAccessExpression): LLVMValueRef {
        let val = this.get_or_codegen_var(var_exp.value_id)
        let ty = this.codegen_type(var_exp.value_type)
        if val == null {
            panic("This should never happen; (codegen_var_access_exp)")
        }
        if var_exp.value_id.kind == VarIdKind.FNC
            return val
        if this.in_static_ctx
            return LLVMGetInitializer(val)
        return LLVMBuildLoad2(builder, ty, val, "")
    }
    private codegen_null(this, null_exp: NullExpression): LLVMValueRef{
        return LLVMConstNull(this.codegen_type(null_exp.value_type))
    }
    private codegen_ternary_exp(this, builder: LLVMBuilderRef, ternary_exp: TernaryExpression): LLVMValueRef {
        let true_block = LLVMAppendBasicBlock(this.function, "true_block")
        let false_block = LLVMAppendBasicBlock(this.function, "false_block")
        let end = LLVMAppendBasicBlock(this.function, "end")
        let cond = this.codegen_expression(builder, ternary_exp.cond)
        LLVMBuildCondBr(builder, cond, true_block, false_block)
        LLVMPositionBuilderAtEnd(builder, true_block)
        let is_true = this.codegen_expression(builder, ternary_exp.is_true)
        LLVMBuildBr(builder, end)
        LLVMPositionBuilderAtEnd(builder, false_block)
        let is_false = this.codegen_expression(builder, ternary_exp.is_false)
        LLVMBuildBr(builder, end)
        LLVMPositionBuilderAtEnd(builder, end)
        let phi_node = LLVMBuildPhi(builder, this.codegen_type(ternary_exp.is_true.value_type), "ternary")
        let incomming_vals: LLVMValueRef[2] = [is_true, is_false]
        let blocks: LLVMBasicBlockRef[2] = [true_block, false_block]
        LLVMAddIncoming(phi_node, &incomming_vals[0], &blocks[0], blocks.size)
        return phi_node
    }
    private codegen_match_case(this, builder: LLVMBuilderRef, switch: LLVMValueRef, expression: Expression, block: LLVMBasicBlockRef){
        if expression.expr_kind == ExpressionKind.BIN {
            let bin_expression = expression as BinaryExpression
            if bin_expression.op == BinaryOp.OR {
                this.codegen_match_case(builder, switch, bin_expression.lhs, block)
                this.codegen_match_case(builder, switch, bin_expression.rhs, block)
                return
            }
        }
        LLVMAddCase(switch, this.codegen_expression(builder, expression), block)
    }
    private codegen_match_exp(this, builder: LLVMBuilderRef, match_exp: MatchExpression): LLVMValueRef {
        let else_block = LLVMAppendBasicBlock(this.function, "match.else")
        let end_block = LLVMAppendBasicBlock(this.function, "match.end")
        let cases_size = match_exp.cases.length + 1
        let cases_blocks: LLVMBasicBlockRef*(cases_size)
        let yield_values: LLVMValueRef*(cases_size)
        let idx = 0
        let curr_block: LLVMBasicBlockRef = null
        let match_type = this.codegen_type(match_exp.value_type)
        let subject = this.codegen_expression(builder, match_exp.subject)
        let switch: LLVMValueRef = match_exp.use_ifs ? null : LLVMBuildSwitch(builder, subject, else_block, idx)
        for case in match_exp.cases {
            if curr_block != null {
                LLVMPositionBuilderAtEnd(builder, curr_block)
            }
            let case_block = LLVMAppendBasicBlock(this.function, "match.case")
            if case.kind == MatchCaseKind.EEQ and (switch != null) {
                this.codegen_match_case(builder, switch, case.condition, case_block)
            } else {
                curr_block = (idx == match_exp.cases.length - 1) ? else_block : LLVMAppendBasicBlock(this.function, "match.case.else")
                LLVMBuildCondBr(builder, this.codegen_expression(builder, case.condition), case_block, curr_block)
            }
            LLVMPositionBuilderAtEnd(builder, case_block)
            let yield_value = this.codegen_block(builder, case.block)
            LLVMBuildBr(builder, end_block)
            cases_blocks[idx] = case_block
            yield_values[idx++] = yield_value
        }
        LLVMPositionBuilderAtEnd(builder, else_block)
        cases_blocks[idx] = else_block
        yield_values[idx++] = this.codegen_block(builder, match_exp.else_block)
        LLVMBuildBr(builder, end_block)
        LLVMPositionBuilderAtEnd(builder, end_block)
        if match_exp.value_type.kind != TypeKind.VOID {
            let phi_node = LLVMBuildPhi(builder, match_type, "match.phi.node")
            LLVMAddIncoming(phi_node, yield_values, cases_blocks, idx)
            return phi_node
        }
        return null
    }
    /* TODO: Needs refactoring to remove array expression */
    private codegen_call_exp(this, builder: LLVMBuilderRef, call_exp: CallExpression): LLVMValueRef {
        let func: LLVMValueRef = null
        let args_value: LLVMValueRef[] = []
        if call_exp.callee.expr_kind == ExpressionKind.MEMBER {
            let member_exp = call_exp.callee as MemberExpression
            if !get_method(this.program, member_exp.member_id).is_static {
                let struct_class = this.get_or_codegen_class_from_typeid(member_exp.member_id.type_id)
                let self = this.codegen_expression(builder, member_exp.expression)
                func = this.codegen_member_exp(builder, member_exp, self)
                args_value << self
            }
        }
        if func == null {
            if call_exp.callee.expr_kind == ExpressionKind.ACCESS {
                let super_id = (call_exp.callee as VarAccessExpression).value_id
                let this_param = this.super_functions[create_var_key(super_id)]
                if this_param != null {
                    args_value << this_param
                }
            }
            func = this.codegen_expression(builder, call_exp.callee)
        }
        for call_arg in call_exp.args {
            args_value << this.codegen_expression(builder, call_arg)
        }
        return LLVMBuildCall2(builder, this.codegen_type(call_exp.callee.value_type, false), func, args_value.buffer, args_value.length, "")
    }
    private codegen_index_exp(this, builder: LLVMBuilderRef, index_exp: IndexExpression): LLVMValueRef {
        let ptr = this.get_expr_mem(builder, index_exp)
        return LLVMBuildLoad2(builder, this.codegen_type(index_exp.value_type), ptr, "")
    }
    private codegen_new_exp(this, builder: LLVMBuilderRef, new_expr: NewExpression): LLVMValueRef {
        let ptr: LLVMValueRef = null
        let alloc_ty = new_expr.memory_type
        let llvm_type: LLVMTypeRef = null
        if is_pointer(new_expr.value_type) {
            llvm_type = this.codegen_type(alloc_ty)
            let size = this.codegen_expression(builder, new_expr.args[0])
            if new_expr.onheap {
                ptr = LLVMBuildArrayMalloc(builder, llvm_type, size, "")
            } else {
                ptr = LLVMBuildArrayAlloca(builder, llvm_type, size, "")
            }
        } else if is_object(alloc_ty) {
            let args_length = new_expr.args.length + 1
            let args: LLVMValueRef*(args_length)
            llvm_type = this.codegen_type(alloc_ty, false)
            if new_expr.onheap {
                ptr = LLVMBuildMalloc(builder, llvm_type, "")
            } else {
                ptr = LLVMBuildAlloca(builder, llvm_type, "")
            }
            args[0] = ptr
            this.evaluate_expression_list(builder, args, 1, new_expr.args)
            this.build_constructor_call(builder, alloc_ty as ObjectType, args, args_length)
        }
        return ptr
    }
    private codegen_member_exp(this, builder: LLVMBuilderRef, member_exp: MemberExpression, self: LLVMValueRef): LLVMValueRef {
        if is_object(member_exp.expression.value_type) {
            let ptr = this.get_member_expr_mem(builder, member_exp, self)
            if is_function(member_exp.value_type)
                return ptr
            return LLVMBuildLoad2(builder, this.codegen_type(member_exp.value_type), ptr, "")
        }
        let var_id = (member_exp.expression as VarAccessExpression).value_id
        let prop_id_key = create_prop_key(member_exp.member_id)
        if var_id.kind == VarIdKind.CLASS {
            let struct_class = this.get_or_codegen_class_from_typeid(ClassVarId.to_classId(var_id))
            if struct_class == null {
                panic("this should not happen; (codegen_member_exp); class")
            }
            let prop = struct_class.static_props_map[prop_id_key]
            if prop == null {
                panic("This should never happen; (codegen_member_expr; class prop)")
            }
            if is_function(member_exp.value_type)
                return prop
            return LLVMBuildLoad2(builder, this.codegen_type(member_exp.value_type), prop, "")
        }
        if var_id.kind == VarIdKind.ENUM {
           let enum_type = this.get_or_codegen_enum_from_typeid(EnumVarId.to_enumId(var_id)) 
             if enum_type == null {
                panic("this should not happen; (codegen_member_exp); enum")
            }
            return enum_type.field_map[prop_id_key]
        }
        return null
    }
    private codegen_string_to_cstring(this, builder: LLVMBuilderRef, value: LLVMValueRef, range: FloRange): LLVMValueRef {
        let string_class_id = get_scope(this.program, this.program.modules[0].scope).classes["String"]
        let string_obj_ty: ObjectType(string_class_id)
        let prop_ty: FunctionType
        let func = this.codegen_get_member_with_name(builder, value, string_obj_ty, "to_cstring", range, &prop_ty)
        let args: LLVMValueRef[1] = [value] 
        return LLVMBuildCall2(builder, this.codegen_type(prop_ty, false), func, &args[0], args.size, "")
    }
    private codegen_intrinsic_exp(this, builder: LLVMBuilderRef, intrinsic_call_exp: IntrinsicCall): LLVMValueRef {
        let intrinsic_type = intrinsic_call_exp.intrinsic
        if intrinsic_type < Intrinsics.MEMCPY {
            let cbuff = this.codegen_string_to_cstring(builder, this.codegen_expression(builder, intrinsic_call_exp.args[0]), intrinsic_call_exp.range)
            let value: LLVMValueRef = null
            if intrinsic_type == Intrinsics.STR_TO_F {
                value = this.codegen_atof_call(builder, cbuff)
            }
            if intrinsic_type == Intrinsics.STR_TO_I {
                value = this.codegen_atoi_call(builder, cbuff)
            }
            if intrinsic_type == Intrinsics.STR_TO_L {
                value = this.codegen_atol_call(builder, cbuff)
            }
            LLVMBuildFree(builder, cbuff)
            return value
        }
        let args: LLVMValueRef*(intrinsic_call_exp.args.length)
        this.evaluate_expression_list(builder, args, 0, intrinsic_call_exp.args)
        let arg0_pointee_ty = (intrinsic_call_exp.args[0].value_type as PointerType).pointee_type
        let arg0_pointee_size = LLVMSizeOf(this.codegen_type(arg0_pointee_ty))
        if intrinsic_type == Intrinsics.MEMRESIZE {
            args[1] = LLVMBuildMul(builder, args[1], arg0_pointee_size, "")
            return this.codegen_realloc_call(builder, args)
        }
        args[2] = LLVMBuildMul(builder, args[2], arg0_pointee_size, "")
        if intrinsic_type == Intrinsics.MEMCMP {
            return this.codegen_memcmp_call(builder, args)
        }

        let arg0_align = LLVMGetAlignment(args[0])
        if intrinsic_type == Intrinsics.MEMSET {
            return LLVMBuildMemSet(builder, args[0], LLVMBuildIntCast2(builder, args[1], LLVMInt8Type(), false, ""), args[2], arg0_align)
        }

        let arg1_align = LLVMGetAlignment(args[1])
        if intrinsic_type == Intrinsics.MEMCPY {
            return LLVMBuildMemCpy(builder, args[0], arg0_align, args[1], arg1_align, args[2])
        }
        if intrinsic_type == Intrinsics.MEMMOVE {
            return LLVMBuildMemMove(builder, args[0], arg0_align, args[1], arg1_align, args[2])
        }
        panic("instrinsic not found!")
        return null
    }
    private codegen_range_exp(this, builder: LLVMBuilderRef, range_exp: RangeExpression): LLVMValueRef {
        let start = this.codegen_expression(builder, range_exp.left)
        let end = this.codegen_expression(builder, range_exp.right)
        let llvm_type = this.codegen_type(range_exp.value_type, false)
        let self = LLVMBuildMalloc(builder, llvm_type, "")
        let args: LLVMValueRef[3] = [self, start, end]
        this.build_constructor_call(builder, range_exp.value_type as ObjectType, &args[0], args.size)
        return self
    }
    private codegen_expression(this, builder: LLVMBuilderRef, expression: Expression): LLVMValueRef {
        this.debug_info.emit_location(builder, expression)
        if expression.expr_kind == ExpressionKind.BOOL
            return this.codegen_bool(expression as BooleanExpression)
        if expression.expr_kind == ExpressionKind.INT
            return this.codegen_int(expression as IntExpression)
        if expression.expr_kind == ExpressionKind.FLOAT
            return this.codegen_float(expression as FloatExpression)
        if expression.expr_kind == ExpressionKind.NULL
            return this.codegen_null(expression as NullExpression)
        if expression.expr_kind == ExpressionKind.STRING
            return this.codegen_string(builder, expression as StringExpression)
        if expression.expr_kind == ExpressionKind.ACCESS
            return this.codegen_var_access_exp(builder, expression as VarAccessExpression)
        if expression.expr_kind == ExpressionKind.BIN 
            return this.codegen_bin_exp(builder, expression as BinaryExpression)
        if expression.expr_kind == ExpressionKind.UNARY
             return this.codegen_unary_exp(builder, expression as UnaryExpression)
        if expression.expr_kind == ExpressionKind.CALL
            return this.codegen_call_exp(builder, expression as CallExpression)
        if expression.expr_kind == ExpressionKind.INDEX
            return this.codegen_index_exp(builder, expression as IndexExpression)
        if expression.expr_kind == ExpressionKind.MEMBER
            return this.codegen_member_exp(builder, expression as MemberExpression, null)
        if expression.expr_kind == ExpressionKind.NEW
            return this.codegen_new_exp(builder, expression as NewExpression)
        if expression.expr_kind == ExpressionKind.TERNARY
            return this.codegen_ternary_exp(builder, expression as TernaryExpression)
        if expression.expr_kind == ExpressionKind.CAST
            return this.codegen_typecast_exp(builder, expression as TypeCastExpression)
        if expression.expr_kind == ExpressionKind.ARRAY
            return this.codegen_array_exp(builder, expression as ArrayExpression)
        if expression.expr_kind == ExpressionKind.TUPLE
            return this.codegen_tuple_exp(builder, expression as TupleExpression)
        if expression.expr_kind == ExpressionKind.MATCH
            return this.codegen_match_exp(builder, expression as MatchExpression)
        if expression.expr_kind == ExpressionKind.INTRINSIC
            return this.codegen_intrinsic_exp(builder, expression as IntrinsicCall)
        if expression.expr_kind == ExpressionKind.RANGE
            return this.codegen_range_exp(builder, expression as RangeExpression)
        return null
    }
    private codegen_if_stmt(this, builder: LLVMBuilderRef, if_stmt: IfStatement){
        let condition = this.codegen_expression(builder, if_stmt.condition)
        let if_bb = LLVMAppendBasicBlock(this.function, "if.entry")
        let else_bb = LLVMAppendBasicBlock(this.function, "else")
        let merge_bb = LLVMAppendBasicBlock(this.function, "ifend")
        LLVMBuildCondBr(builder, condition, if_bb, else_bb)
        LLVMPositionBuilderAtEnd(builder, if_bb)
        this.codegen_block(builder, if_stmt.block)
        let if_terminated = is_terminated(if_stmt.block)
        if !if_terminated
            LLVMBuildBr(builder, merge_bb)

        LLVMPositionBuilderAtEnd(builder, else_bb)
        let else_terminated = false
        if if_stmt.else_block != null {
            this.codegen_block(builder, if_stmt.else_block)
            else_terminated = is_terminated(if_stmt.else_block)
        }
        if !else_terminated
            LLVMBuildBr(builder, merge_bb)
        else if if_terminated
            LLVMDeleteBasicBlock(merge_bb)
    
        LLVMPositionBuilderAtEnd(builder, merge_bb)
    }
    private codegen_for_stmt(this, builder: LLVMBuilderRef, for_stmt: ForStatement){
        let prev_break_bb = this.break_bb
        let prev_continue_bb = this.continue_bb
        let for_entry_block = LLVMAppendBasicBlock(this.function, "for.entry")
        LLVMBuildBr(builder, for_entry_block)

        LLVMPositionBuilderAtEnd(builder, for_entry_block)
        let for_next_block = LLVMAppendBasicBlock(this.function, "for.next")
        let for_body_block = LLVMAppendBasicBlock(this.function, "for.body")
        let for_end_block = LLVMAppendBasicBlock(this.function, "for.end")

        let iterator = this.codegen_expression(builder, for_stmt.iterator)
        let iter_func_type: FunctionType
        let iter_func = this.codegen_get_member_with_name(builder, iterator, for_stmt.iterator.value_type as ObjectType, "__iter__", for_stmt.iterator.range, &iter_func_type)
        let args: LLVMValueRef[1] = [iterator]
        this.codegen_var_declaration(builder, for_stmt.it_var)
        let iterable_type = iter_func_type.return_type as ObjectType
        let iteratable = LLVMBuildCall2(builder, this.codegen_type(iter_func_type, false), iter_func, &args[0], args.size, "")
        args[0] = iteratable
        let next_func_type: FunctionType
        let next_func = this.codegen_get_member_with_name(builder, iteratable, iterable_type, "next", for_stmt.iterator.range, &next_func_type)
        LLVMBuildBr(builder, for_next_block)

        LLVMPositionBuilderAtEnd(builder, for_next_block)
        let next_val = LLVMBuildCall2(builder, this.codegen_type(next_func_type, false), next_func, &args[0], args.size, "")
        let next_var_mem = this.get_var_from_varid(for_stmt.it_var.var_id)
        let iter_done = this.codegen_get_member_with_name(builder, iteratable, iterable_type, "done", for_stmt.iterator.range)
        LLVMBuildStore(builder, next_val, next_var_mem)
        LLVMBuildCondBr(builder, iter_done, for_end_block, for_body_block)
        
        LLVMPositionBuilderAtEnd(builder, for_body_block)
        this.break_bb = for_end_block
        this.continue_bb = for_next_block
        this.codegen_block(builder, for_stmt.block)
        if !is_terminated(for_stmt.block)
            LLVMBuildBr(builder, for_next_block)

        LLVMPositionBuilderAtEnd(builder, for_end_block)
        LLVMBuildFree(builder, iteratable)
        this.break_bb = prev_break_bb
        this.continue_bb = prev_continue_bb
    }
    private codegen_while_stmt(this, builder: LLVMBuilderRef, while_stmt: WhileStatement){
        let while_entry_bb = LLVMAppendBasicBlock(this.function, "while.entry")
        let while_end_bb = LLVMAppendBasicBlock(this.function, "while.end")
        let while_body_bb = LLVMAppendBasicBlock(this.function, "while.body")
        
        let prev_break_bb = this.break_bb
        let prev_continue_bb = this.continue_bb
        this.break_bb = while_end_bb
        this.continue_bb = while_entry_bb
        LLVMBuildBr(builder, while_entry_bb)

        LLVMPositionBuilderAtEnd(builder, while_entry_bb)
        let condition = this.codegen_expression(builder, while_stmt.condition)
        LLVMBuildCondBr(builder, condition, while_body_bb, while_end_bb)

        LLVMPositionBuilderAtEnd(builder, while_body_bb)
        this.codegen_block(builder, while_stmt.block)
        if !is_terminated(while_stmt.block) {
            LLVMBuildBr(builder, while_entry_bb)   
        }

        LLVMPositionBuilderAtEnd(builder, while_end_bb)
        this.break_bb = prev_break_bb
        this.continue_bb = prev_continue_bb
    }
    private evaluate_expression_list(this, builder: LLVMBuilderRef, evaluated_list: LLVMValueRef*, start: int, list: Expression[]){
        for item in list {
            evaluated_list[start++] = this.codegen_expression(builder, item)
        }
    }
    private build_constructor_call(this, builder: LLVMBuilderRef, obj_type: ObjectType, params: LLVMValueRef*, params_length: uint){
        let checked_class = get_class_from_type(this.program, obj_type)
        let constructor = this.get_or_codegen_class_from_typeid(obj_type.class_id).class_constructor
        if constructor != null {
            LLVMBuildCall2(builder, LLVM_GetFunctionType(constructor), constructor, params, params_length, "")
        } else {
            panic("Could not find constructor for constructor call")
        }
    }
    private codegen_var_declaration(this, builder: LLVMBuilderRef, var_decl: VarDeclaration){
        let var_id = var_decl.var_id
        let var = get_var(this.program, var_id)
        let expression = var.value
        if var_decl.is_static {
            let name = var_decl.name.to_cstring()
            let expression_val_type = var.value_type
            let ty = this.codegen_type(expression_val_type)
            let ptr = LLVMAddGlobal(this.module, ty, name)
            if expression != null {
                let value = this.codegen_expression(builder, expression)
                LLVMSetInitializer(ptr, value)
                if(var_id.kind == VarIdKind.CONST) {
                    LLVMSetGlobalConstant(ptr, true)
                }
            }
            this.add_var(var_id, ptr)
        } else {
            let ptr = this.get_var_from_varid(var_id)
            let var_type = get_var(this.program, var_id).value_type
            let dbg_var = this.debug_info.create_auto_variable(var_id.module_id, var_decl.name, var_type, var_decl.range)
            this.debug_info.insert_declare_before(ptr, dbg_var)
            if expression != null {
                this.store_expr_to_mem(builder, ptr, expression)
            }
        }
    }
    private codegen_statement(this, builder: LLVMBuilderRef, statement: Statement){
        if statement.kind == StatementKind.IF
            this.codegen_if_stmt(builder, statement as IfStatement)
        if statement.kind == StatementKind.FOR
            this.codegen_for_stmt(builder, statement as ForStatement)
        if statement.kind == StatementKind.WHILE
            this.codegen_while_stmt(builder, statement as WhileStatement)
        if statement.kind == StatementKind.CONTINUE
            LLVMBuildBr(builder, this.continue_bb)
        if statement.kind == StatementKind.BREAK
            LLVMBuildBr(builder, this.break_bb)
        if statement.kind == StatementKind.VAR
            this.codegen_var_declaration(builder, statement as VarDeclaration)
        if statement.kind == StatementKind.RETURN
            this.codegen_return_stmt(builder, statement as ReturnStatement)
        if statement.kind == StatementKind.EXPRESSION
            this.codegen_expression(builder, statement as Expression)
    }
    private codegen_block(this, builder: LLVMBuilderRef, block: Block, lexical_block: LLVMMetadataRef = null): LLVMValueRef {
        let was_instatic_ctx = this.in_static_ctx
        if(lexical_block == null) lexical_block = this.debug_info.create_lexical_block(block)
        this.in_static_ctx = false
        this.debug_info.lexical_blocks << lexical_block
        for statement in block.statements {
            this.codegen_statement(builder, statement)
        }
        if block.yields != null {
           return this.codegen_expression(builder, block.yields)
        }
        this.debug_info.lexical_blocks.pop()
        this.in_static_ctx = was_instatic_ctx
        return null
    }
    private codegen_vars_alloc_function_scope(this, builder: LLVMBuilderRef, scope: ScopeId){
        let current_scope = get_scope(this.program, scope)
        for var_entry in current_scope.vars {
            let var_name = var_entry.key
            let var_id = var_entry.value
            let var_value_type: Type
            if var_id.kind == VarIdKind.VAR or var_id.kind == VarIdKind.CONST {
                var_value_type = get_var(this.program, var_id).value_type
            } else if var_id.kind == VarIdKind.FNC {
                var_value_type = get_func(this.program, var_id).fnc_type
            }
            let ptr = LLVMBuildAlloca(builder, this.codegen_type(var_value_type), "")
            this.add_var(var_id, ptr)
        }
        for child_scope in current_scope.children {
            this.codegen_vars_alloc_function_scope(builder, child_scope)
        }
    }
    private codegen_function(this, func: Function){
        let fn_name = func.name.to_cstring()
        let llvm_fnc = LLVMAddFunction(this.module, fn_name, this.codegen_type(func.fnc_type, false))
        this.add_var(func.id, llvm_fnc)
        if func.is_external { 
            LLVMSetLinkage(llvm_fnc, LLVMLinkage.LLVMExternalLinkage)
            return
        }
        let fnc_dbg = this.debug_info.create_function(func)
        LLVMSetSubprogram(llvm_fnc, fnc_dbg)

        let func_builder = LLVMCreateBuilder()
        LLVMPositionBuilderAtEnd(func_builder, LLVMAppendBasicBlock(llvm_fnc, ""))
        this.codegen_vars_alloc_function_scope(func_builder, func.scope)
        for elem in 0..func.args.length {
            let i = elem as uint
            let arg = func.args[i]
            let alloc = this.get_var_from_varid(arg.id)
            let param = LLVMGetParam(llvm_fnc, i)
            LLVMBuildStore(func_builder, param, alloc)

            let var_dbg = this.debug_info.create_param_variable(fnc_dbg, i, arg)
            this.debug_info.insert_declare_before(alloc, var_dbg)
        }
        let prev_func = this.function
        this.function = llvm_fnc
        this.codegen_block(func_builder, func.block, fnc_dbg)
        LLVMDIBuilderFinalizeSubprogram(this.debug_info.builder, fnc_dbg)
        LLVMDisposeBuilder(func_builder)
        this.function = prev_func
    }
    private codegen_class(this, checked_class: Class){
        let struct_class = this.get_class_from_typeid(checked_class.id)
        for method in checked_class.methods {
            if method.is_abstract continue
            let llvm_method = LLVMGetNamedFunction(this.module, "$(checked_class.name)_$(method.name)".to_cstring())           
            let is_constructor = method.name == "constructor"

            let method_dbg = this.debug_info.create_method(method)
            LLVMSetSubprogram(llvm_method, method_dbg)
            
            let entry_block = LLVMAppendBasicBlock(llvm_method, "")
            let func_builder = LLVMCreateBuilder()
            LLVMPositionBuilderAtEnd(func_builder, entry_block)
            this.codegen_vars_alloc_function_scope(func_builder, method.scope)
            let offset = 0
            if !method.is_static {
                let this_param = LLVMGetParam(llvm_method, 0)
                let this_id = get_scope(this.program, method.scope).vars["this"]
                let this_alloc = this.get_var_from_varid(this_id)
                LLVMBuildStore(func_builder, this_param, this_alloc)
                if is_constructor {
                    let parent_ty = checked_class.parent as ObjectType
                    if parent_ty != null {
                        let super_id = get_scope(this.program, method.scope).vars["super"]
                        if super_id != null {
                            let super_constructor = this.get_or_codegen_class_from_typeid(parent_ty.class_id).class_constructor
                            let super_alloc = this.get_var_from_varid(super_id)
                            LLVMBuildStore(func_builder, super_constructor, super_alloc)
                            this.super_functions[create_var_key(super_id)] = this_param
                        }
                    }
                }
                offset = 1
            }
            for elem in 0..method.args.length {
                let i = elem as uint
                let param = LLVMGetParam(llvm_method, i + offset)
                let arg = method.args[i]
                let param_alloc = this.get_var_from_varid(arg.id)
                LLVMBuildStore(func_builder, param, param_alloc)

                let var_dbg = this.debug_info.create_param_variable(method_dbg, i, arg)
                this.debug_info.insert_declare_before(param_alloc, var_dbg)
            }
            let prev_func = this.function
            this.function = llvm_method
            this.codegen_block(func_builder, method.block, method_dbg)
            LLVMDIBuilderFinalizeSubprogram(this.debug_info.builder, method_dbg)
            LLVMDisposeBuilder(func_builder)
            this.function = prev_func
        }
    }
    private declare_method(this, method_name: String, method_type: LLVMTypeRef): LLVMValueRef {
        return LLVMAddFunction(this.module, method_name.to_cstring(), method_type)
    }
    private declare_static_props(this, struct_class: CGClassType, checked_class: Class){
        for entry in checked_class.static_prop_map {
            let prop_id = entry.value
            let static_prop_name = entry.key
            let prop_id_key = create_prop_key(prop_id)
            let prop_type = get_class_prop_type(this.program, prop_id)
            let static_prop_type = this.codegen_type(prop_type, is_object(prop_type))
            let static_prop_name_concat = "$(checked_class.name)_$static_prop_name"
            let static_prop_value: LLVMValueRef = null
            if prop_id.kind == PropIdKind.METHOD {
                static_prop_value = this.declare_method(static_prop_name_concat, static_prop_type)
            } else {
                static_prop_value = LLVMAddGlobal(this.module, static_prop_type, static_prop_name_concat.to_cstring())
                let global_prop_initializer = this.codegen_expression(null, checked_class.fields[prop_id.id].initializer)
                LLVMSetInitializer(static_prop_value, global_prop_initializer)
            }
            struct_class.static_props_map[prop_id_key] = static_prop_value
        }
    }
    private declare_class_props(this, struct_class: CGClassType, checked_class: Class, prop_types: LLVMTypeRef[]){
        let field_types: LLVMTypeRef[] = []
        for entry in checked_class.prop_map {
            let prop_id = entry.value
            let prop_name = entry.key
            let prop_owner = checked_class
            let prop_id_key = create_prop_key(prop_id)
            let prop_type = get_class_prop_type(this.program, prop_id)
            if prop_id.kind == PropIdKind.FIELD {
                let field_offset = get_field_offset(this.program, prop_id)
                let field_type = this.codegen_type(prop_type)
                struct_class.fields_pos_map[prop_id_key] = field_offset
                let offset_cap = field_offset + 1
                field_types.ensure_capacity(offset_cap)
                field_types[field_offset] = field_type
                if (offset_cap > field_types.length) {
                    field_types.length = offset_cap
                }
            } else {
                let method_type = prop_type as FunctionType
                let llvm_method_val: LLVMValueRef = null
                let llvm_method_type: LLVMTypeRef = null
                let arg_types: Type[] = [new ObjectType(checked_class.id)]
                arg_types += method_type.arg_types
                del method_type.arg_types
                method_type.arg_types = arg_types
                llvm_method_type = this.codegen_type(method_type, false)
                if !checked_class.methods[prop_id.id].is_abstract {
                    llvm_method_val = this.declare_method("$(checked_class.name)_$prop_name", llvm_method_type)
                }
                struct_class.static_props_map[prop_id_key] = llvm_method_val
            }
        }
        prop_types += field_types
        del field_types
    }
    private declare_class(this, checked_class: Class){
        let struct_class = new CGClassType()
        struct_class.llvm_type = LLVMStructCreateNamed(LLVMGetModuleContext(this.module), checked_class.name.to_cstring())
        this.add_class(checked_class.id, struct_class)
        let prop_types: LLVMTypeRef[] = []
        // Taking care of static methods.
        this.declare_static_props(struct_class, checked_class)
        this.declare_class_props(struct_class, checked_class, prop_types)
        LLVMStructSetBody(struct_class.llvm_type, prop_types.buffer, prop_types.length, true)
        del prop_types
        // Now constructor
        if checked_class.construct != null {
            let constructor_type = get_class_prop_type(this.program, checked_class.construct) as FunctionType
            let arg_types: Type[] = [new ObjectType(checked_class.id)]
            arg_types += constructor_type.arg_types
            del constructor_type.arg_types
            constructor_type.arg_types = arg_types
            let constructor_llvm_method_type = this.codegen_type(constructor_type, false)
            struct_class.class_constructor = this.declare_method("$(checked_class.name)_constructor", constructor_llvm_method_type)
        } else {
            struct_class.class_constructor = null
        }
    }
    private declare_enum(this, checked_enum: Enum){
        let enum_type = new CGEnumType()
        if checked_enum.fields.length > 0 {
            enum_type.llvm_type = this.codegen_type(checked_enum.fields[0].initializer.value_type)
        } else {
            enum_type.llvm_type = LLVMIntType(ARCH_WORD_SIZE)
        }
        let elements: LLVMValueRef*(checked_enum.fields.length)
        let idx = 0
        for enum_field in checked_enum.fields {
            let prop_id = enum_field.id
            enum_type.field_map[create_prop_key(prop_id)] = this.codegen_expression(null, enum_field.initializer)
            let str = "$(checked_enum.name).$(enum_field.name)"
            let str_const = LLVMConstString(str.to_cstring(), str.length, false) //Issue: Mem lick!
            let str_global = LLVMAddGlobal(this.module, LLVMTypeOf(str_const), "")
            LLVMSetInitializer(str_global, str_const)
            elements[idx++] = str_global
        }
        let metanames_initializer = LLVMConstArray(LLVMPointerType(LLVMInt8Type()), elements, idx)
        let metanames = LLVMAddGlobal(this.module, LLVMTypeOf(metanames_initializer), "$(checked_enum.name)_names".to_cstring())
        LLVMSetInitializer(metanames, metanames_initializer)
        enum_type.metanames = metanames
        this.add_enum(checked_enum.id, enum_type)
    }
}