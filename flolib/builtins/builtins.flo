const STDIN = 0
const STDOUT = 1
const STDERR = 2
/*@extern*/
fnc strlen(str: i8*): int
fnc write(fc: int, buf: i8*, len: int)
fnc read(fd: int, buff: i8*, size: int): int 
// ----- functions -----
fnc print(str: string){
    write(STDOUT, str.get_bytes(), str.length)
}
fnc println(str: string){
    print(str)
    write(STDOUT, "\n", 1)
}
fnc eprint(str: string){
    write(STDERR, str.get_bytes(), str.length)
}
fnc eprintln(str: string){
    eprint(str)
    write(STDERR, "\n", 1)
}
fnc input(): string {
    let size = -1
    let buffer = new i8*(1)
    while (buffer[size] != '\n'){
        buffer = buffer.resize((++size)+1)
        read(STDIN, buffer+size, 1)
    }
    return new string(buffer, size)
}
// ----- string -------
class string {
    private buffer: i8*
    public length: int
    private size: int
    constructor(this, data: i8*, len: int){
        this.buffer = data
        this.length = len
        this.size = len
    }
    private __add__(this, data: string): string {
        let new_str_len = this.length + data.length
        let new_buffer = new i8*(new_str_len)
        new_buffer.copy_from(this.buffer, this.length)
        let trailing_buffer = new_buffer + this.length
        trailing_buffer.copy_from(data.buffer, data.length)
        return new string(new_buffer, new_str_len)   
    }
    private __adda__(this, other: string): string {
        let new_length = this.length + other.length
        this.buffer = this.buffer.resize(new_length)
        (this.buffer + this.length).copy_from(other.buffer, other.length)
        this.length = new_length
        return this
    }
    private __getitem__(this, index: int): string {
        let new_buffer = new i8*(1)
        new_buffer[0] = this.buffer[index]
        return new string(new_buffer, 2)
    }
    private __ne__(this, other: string): bool {
        return !(this.__eq__(other))
    }
    private __eq__(this, other: string): bool {
        if this.length != other.length return false
        return this.buffer.compare(other.buffer, this.length) == 0
    }
    private __in__(this, other: string): bool {
        return this.find(other) != -1
    }
    public find(this, search: string, start: int = 0): int {
        for let i = start; i < this.length; i++ {
            if (search.buffer[0] == this.buffer[i]) {
                let x = search.length - 1
                let matches = true
                while x > 0 {
                    if (search.buffer[x] != this.buffer[x+i]){
                        return -1
                    }
                    x--
                }
                if matches return i
            }
        }
        return -1
    }
    public substring (this, start: int, length: int): string {
        return new string(this.buffer + start, length)
    }
    public get_byte(this, index: int): i8 {
        return (this.buffer[index])
    }
    public get_bytes(this): i8* {
        return this.buffer
    }
    public replace(this, val: string, repl: string): string {
        let idx = this.find(val)
        if idx >= 0 {
            let nlen = this.length - val.length + repl.length
            let nbuff = new i8*(nlen)
            nbuff.copy_from(this.buffer, idx)
            (&nbuff[idx]).copy_from(repl.buffer, repl.length)
            (&nbuff[idx + repl.length]).copy_from(&this.buffer[idx + val.length], this.length - (idx + val.length))
            return new string(nbuff, nlen)
        } 
        return this
    }
    public to_cstring(this): i8* {
        let c_string = new i8*(this.length + 1)
        c_string.copy_from(this.buffer, this.length)
        c_string[this.length] = '\0'
        return c_string
    }
    public ends_with(this, suffix: string): bool {
        if (this.length < suffix.length) return false
        let end_buff = this.buffer + this.length - suffix.length
        return end_buff.compare(suffix.buffer, suffix.length) == 0
    }
    public starts_with(this, preffix: string): bool {
        if (this.length < preffix.length) return false
        return this.buffer.compare(preffix.buffer, preffix.length) == 0
    }
    public from_cstring(str: i8*): string {
        let len = strlen(str)
        let buf = new i8*(len)
        buf.copy_from(str, len)
        return new string(buf, len)
    }
    public from_bytes(bytes: i8*, len: int): string {
        let buf = new i8*(len)
        buf.copy_from(bytes, len)
        return new string(buf, len)
    }
}
class StringBuilder {
    private buffer: i8*
    private size: int
    private working_buffer: i8*
    public length: int
    constructor(this, size: int){
        this.size = size
        this.buffer = new i8*(this.size)
        this.working_buffer = this.buffer
        this.length = 0
    }
    private resize(this){
        this.size *= 2
        this.buffer = this.buffer.resize(this.size)
        this.working_buffer = this.buffer + this.length
    }
    public add_bytes_length(this, bytes: i8*, length: int){
        if (this.size - this.length <= length) this.resize()
        this.working_buffer.copy_from(bytes, length)
        this.working_buffer += length
        this.length += length
    }
    public append_string(this, str: string){
        this.add_bytes_length(str.get_bytes(), str.length)
    }
    public add_byte(this, byte: i8){
        if (this.size - this.length <= 1) this.resize()
        this.working_buffer[0] = byte
        this.working_buffer += 1
        this.length += 1
    }
    public get_string(this): string {
        return new string(this.buffer, this.length)
    }
}
// ------- Range -------
class RangeIterator {
    private done: bool
    private idx: int
    private end: int
    constructor(this, idx: int, end: int){
        this.idx = idx
        this.end = end
    }
    private next(this): int {
        this.done = this.idx >= this.end
        return this.idx++
    }
}
class Range {
    private start: int
    private end: int
    constructor(this, start: int, end: int){
        this.start = start
        this.end = end
    }
    private __in__(this, num: int): bool {
        return num >= this.start and num < this.end
    }
    private __iter__(this): RangeIterator{
        return new RangeIterator(this.start, this.end)
    }
}
// ----- Array -------
class Array <T> {
    public buffer: T*
    public length: int
    private size: int
    constructor(this, size: int){
        this.length = 0
        this.size = size
        this.buffer = new T*(size)
    }
    private resize(this){
        this.size *= 2
        this.buffer = this.buffer.resize(this.size)
    }
    private __iter__(this): ArrayIterator<T> {
        return new ArrayIterator<T>(this.buffer, this.length)
    } 
    private __sl__(this, element: T){
        if (this.length >= this.size) this.resize()
        this.buffer[this.length++] = element
    }
    private __setitem__(this, index: int, value: T){
        this.buffer[index] = value
    }
    private __getitem__(this, index: int): T {
        return this.buffer[index]
    }
    private __eq__(this, other: T[]): bool {
        if this.length != other.length {
            return false
        } else {
           for let i = 0; i < this.length; i++ {
               if this[i] != other[i] return false
           }
        }
        return true
    }
    private __in__(this, element: T): bool {
        return this.find(element) != -1
    }
    public find(this, search: T): int{
        for let i = 0; i < this.length; i++ {
            if this.buffer[i] == search return i
        }
        return -1
    }
}
class ArrayIterator<T> {
    private done: bool
    private idx: int
    private end: int
    private bytes: T*
    constructor(this, bytes: T*, end: int){
        this.done = false
        this.idx = 0
        this.bytes = bytes
        this.end = end
    }
    private next(this): T {
        let next_elem = this.bytes[this.idx++]
        this.done = this.idx > this.end
        return next_elem
    }
}
// ----- Map -------
class Bucket<T, U> {
    key: T
    value: U
    is_empty: bool
    constructor(this){
        this.is_empty = true
    }
    set(this, key: T, value: U){
        this.is_empty = false
        this.key = key
        this.value = value
    }
}
class Map<T, U> {
    private buckets: Bucket<T, U>*
    private length: int
    private size: int
    constructor(this){
        let initial_capacity = 256
        this.buckets = new Bucket<T, U>*(initial_capacity)
        for let i = 0; i < initial_capacity; i++ {
            this.buckets[i] = new Bucket<T, U>()
        }
        this.size = initial_capacity
        this.length = 0
    }
    private hash_fnc(this, key: T): int {
        let str = key as string
        let hash = 0
        for let i = 0; i < str.length; ++i {
            hash = (hash << 1) + (str.get_byte(i) as int)
        }
        return hash < 0 ? -hash : hash
    }
    private resize(this){
        let buckets = this.get_buckets()
        this.size *= 2
        this.length = 0
        this.buckets = this.buckets.resize(this.size)
        for let i = 0; i < this.size; i++{
            this.buckets[i] = new Bucket<T, U>()
        }
        for let i = 0; i < buckets.length; i++ {
            this.__setitem__(buckets[i].key, buckets[i].value)
        }
    }
    private __setitem__(this, key: T, value: U){
        let ratio = this.length / (this.size as float)
        if (ratio > 0.75) this.resize()
        let hash = this.hash_fnc(key) % this.size
        while !this.buckets[hash].is_empty {
            if this.buckets[hash].key == key
                break
            hash = (hash + 1) % this.size
        } 
        this.length++
        this.buckets[hash].set(key, value)
    }
    private __getitem__(this, key: T): U {
        let hash = this.hash_fnc(key) % this.size
        while !this.buckets[hash].is_empty {
            if this.buckets[hash].key == key
                break
            else
                hash = (hash + 1) % this.size
        }
        if !this.buckets[hash].is_empty
            return this.buckets[hash].value
        return null
    }
    private __iter__(this): MapIterator<T, U> {
        return new MapIterator<T, U>(this.buckets, this.size, this.length)
    }
    public get_buckets(this): Bucket<T, U>[] {
        let buckets: Bucket<T, U>[] = []
        for let i = 0; i < this.size; ++i {
            let current_bucket = this.buckets[i]
            if !current_bucket.is_empty {
                buckets << current_bucket
            }
        }
        return buckets  
    }
}
class MapIterator<T, U> {
    idx: int
    done: bool
    buckets: Bucket<T, U>*
    size: int
    length: int
    constructor(this, buckets: Bucket<T, U>*, size: int, length: int){
        this.idx = 0
        this.done = false
        this.buckets = buckets
        this.size = size
        this.length = length
    }
    private next(this): Bucket<T, U> {
        let i = 0
        for i = this.idx; i < this.size; i++ {
            if !this.buckets[i].is_empty {
                break
            }
        }
        this.done = this.idx++ > this.length
        return this.buckets[i]
    }
}
