import socket, connect, accept, bind, listen, close in "@flo/sys/lib"
fnc inet_addr(addr: i8*): int
fnc htons(x: int): int {
    let x1 = (x and 255) << 8
    let x2 = (x >> 8) and 255 
    return x1 or x2
}
fnc calc_sin_addr(sin_family: int, sin_port: int): int {
    return (sin_port << 16) or (sin_family and 0xFFFF)
}
class Socket {
    sockfd: int
    domain: int
    constructor(this, domain: int, socktype: int, protocol: int){
        let sockfd = socket(domain, socktype, protocol)
        if sockfd < 1 {
            println("Error: could not create TCP socket")
            return
        }
        this.sockfd = sockfd
        this.domain = domain
    }
    bind(this, port: int, address: string = ""){
        let addr: int[2] = [calc_sin_addr(this.domain, htons(port)), inet_addr(address.to_cstring())]
        if bind(this.sockfd, &addr[0] as i8*, 16) < 0 {
            println("Error: could not bind socket")
        }
    }
    connect(this, port: int, address: string){
        let addr: int[2] = [calc_sin_addr(this.domain, htons(port)), inet_addr(address.to_cstring())]
        if connect(this.sockfd, &addr[0] as i8*, 16) < 0 {
            println("Error: could not connect socket")
        }
    }
    listen(this, backlog: int = 10){
        if listen(this.sockfd, backlog) < 0 {
            println("Error: could not start listening")
        }
    }
    close(this){
        close(this.sockfd)
    }
    destroy(this){
        this.close()
    }
}